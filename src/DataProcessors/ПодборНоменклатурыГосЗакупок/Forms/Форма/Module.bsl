#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	СписокСвойств = "ДатаРасчетов, Заголовок, ПодборНоменклатуры,
	                |Контрагент, ДоговорКонтрагента, Организация";
	
	СистемныеНастройкиБазар = ОбщегоНазначенияБазарСервер.СистемныеНастройкиБазар(ТекущаяДата());
	
	НеобязательныеПараметры = Новый Структура;
	
	НеобязательныеПараметры.Вставить("ПоказыватьОстатки"   , Истина);
	НеобязательныеПараметры.Вставить("Курс"                , 0);
	НеобязательныеПараметры.Вставить("Кратность"           , 0);
	НеобязательныеПараметры.Вставить("Склад"               , СистемныеНастройкиБазар.СкладОтгрузки);
	НеобязательныеПараметры.Вставить("ДанныеТабличнойЧасти", Неопределено);
	
	Для каждого НеобязательныйПараметр Из НеобязательныеПараметры Цикл
		
		ИмяПараметра = НеобязательныйПараметр.Ключ;
		
		Если Параметры.Свойство(ИмяПараметра) Тогда
			СписокСвойств = СписокСвойств + "," + ИмяПараметра;
		КонецЕсли; 	
		
	КонецЦикла; 
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НеобязательныеПараметры);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	Если НЕ ПодборНоменклатуры тогда
		ПоказыватьОстатки = Ложь;
		ПоказыватьТолькоОстатки = Ложь;
	КонецЕсли;
	
	// Отключим невыполнимые опции
	Если Не ПравоДоступа("Просмотр", Метаданные.РегистрыБухгалтерии.Хозрасчетный) Тогда
		ПоказыватьОстатки = Ложь;
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	КонецЕсли;
	
	Если Курс = 0 или Кратность = 0 Тогда
		КурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, ДатаРасчетов);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, КурсКратность);
	КонецЕсли;
	
	Настройки = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить("ПодборНоменклатурыГосЗакупок", "");
	Если Настройки <> Неопределено Тогда
		Если Настройки.Свойство("ПоказыватьТолькоОстатки") Тогда
			ПоказыватьТолькоОстатки = Настройки.ПоказыватьТолькоОстатки;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	ИмяТаблицы = "Товары";
	
	Элементы.НоменклатураГосЗакупокВалюта.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет");
	
	УправлениеФормой(ЭтотОбъект);

	УстановитьУсловноеОформление();
	
	ЗаполнитьНоменклатураГосЗакупок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПодборНоменклатуры тогда
		Элементы.ПодобраннаяНоменклатура.ВысотаВСтрокахТаблицы = 3;
		Если ПоказыватьТолькоОстатки И ПоказыватьОстатки тогда
			Если Объект.НоменклатураГосЗакупок.Количество() > 15 тогда
				ТекущийЭлемент = Элементы.СтрокаПоиска;
			Иначе
				ТекущийЭлемент = Элементы.НоменклатураГосЗакупок;
			КонецЕсли;
		Иначе
			ТекущийЭлемент = Элементы.СтрокаПоиска;
		КонецЕсли;
		ГосЗакупкиКлиент.ЗаполнитьСписокВыбораНоменклатурыТаблицыТовары("ПодобраннаяНоменклатураНоменклатураГосЗакупок", Неопределено, ЭтаФорма);
	Иначе
		Элементы.ПодобраннаяНоменклатура.ВысотаВСтрокахТаблицы = 4;
		ТекущийЭлемент = Элементы.СтрокаПоиска;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Параметр = ЭтаФорма Тогда
		Если ИмяСобытия = "ВводНовогоЭлементаСправочникаНоменклатура" Тогда
			Элементы.СписокНоменклатуры.Обновить();
			Элементы.СписокНоменклатуры.ТекущаяСтрока = Источник;
			
			ПараметрыНоменклатуры = Новый Структура;
			ПараметрыНоменклатуры.Вставить("Номенклатура"        , Источник);
			ПараметрыНоменклатуры.Вставить("КоличествоГосЗакупок", 1);
			ПараметрыНоменклатуры.Вставить("ЦенаГосЗакупок"      , 0);
			
			ОткрытьФормуВводаДополнительныхДанных(ПараметрыНоменклатуры);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Объект.ПодобраннаяНоменклатура.Количество() > 0 Тогда
		Отказ = Истина;
	ИначеЕсли Не ПеренестиВДокумент И Объект.ПодобраннаяНоменклатура.Количество() > 0 Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru = 'Подобранные товары не перенесены в документ.
			|
			|Перенести?'");
			
		Оповещение = Новый ОписаниеОповещения("ВопросПеренестиВДокументЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	СтруктураВозврата = ПриЗакрытииНаСервере();
	
	Если ПеренестиВДокумент Тогда
		ОповеститьОВыборе(СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПриЗакрытииНаСервере()
	
	СтруктураВозврата = Новый Структура();
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ПоказыватьТолькоОстатки", ПоказыватьТолькоОстатки);
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("ПодборНоменклатурыГосЗакупок", "", ПараметрыЗакрытия);
	
	Если ПеренестиВДокумент Тогда
		
		Для Каждого ПодобраннаяСтрока из Объект.ПодобраннаяНоменклатура цикл
			ПодобраннаяСтрока.КоэффициентГосЗакупок = ?(ПодобраннаяСтрока.КоэффициентГосЗакупок = 0, 1, ПодобраннаяСтрока.КоэффициентГосЗакупок);
		КонецЦикла;
		
		АдресПодобраннойНоменклатурыВХранилище = ПоместитьПодобраннуюНоменклатуруГосЗакупокВХранилище();
		СтруктураВозврата.Вставить("АдресПодобраннойНоменклатурыВХранилище", АдресПодобраннойНоменклатурыВХранилище);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ПрименитьПоиск();
	ТекущийЭлемент = Элементы.НоменклатураГосЗакупок;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Истина;
	Если Ожидание > 0 тогда
		ПрименитьПоиск(Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоОстаткиПриИзменении(Элемент)
	
	ТекущийЭлемент = Элементы.НоменклатураГосЗакупок;
	УстановитьУсловноеОформление();
	ЗаполнитьНоменклатураГосЗакупок();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();


	// СписокНоменклатурыКоличествоОстаток

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НоменклатураГосЗакупокКоличествоОстаток");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ПоказыватьТолькоОстатки", ВидСравненияКомпоновкиДанных.Равно, ПоказыватьТолькоОстатки);

	//ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьПоиск(ТекстПоиска = Неопределено)
	
	Если ЗначениеЗаполнено(ТекстПоиска) тогда
		ВыражениеПоиска = ТекстПоиска;
	Иначе
		ВыражениеПоиска = СтрокаПоиска;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Наименование", СокрЛП(ВыражениеПоиска));
	Элементы.НоменклатураГосЗакупок.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураПоиска);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВводаДополнительныхДанных(ПередаваемыеПараметры)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьВыбраннуюНоменклатуруГосЗакупокЗавершение", ЭтотОбъект);
	
	ПередаваемыеПараметры.Вставить("ДатаРасчетов", ДатаРасчетов);
	ПередаваемыеПараметры.Вставить("Организация", Организация);
	ПередаваемыеПараметры.Вставить("Склад", Склад);
	ПередаваемыеПараметры.Вставить("Валюта", Валюта);
	
	Если ПодборНоменклатуры тогда
		ОткрытьФорму("Обработка.ПодборНоменклатурыГосЗакупок.Форма.ФормаВводаНоменклатураЦенаКоличество", ПередаваемыеПараметры,
			ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОткрытьФорму("Обработка.ПодборНоменклатурыГосЗакупок.Форма.ФормаВводаЦенаКоличество", ПередаваемыеПараметры,
			ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВыбраннуюНоменклатуруГосЗакупокЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПодборНоменклатуры тогда
		ПараметрыПоиска = Новый Структура("НоменклатураГосЗакупок, Номенклатура, ЦенаГосЗакупок", Результат.НоменклатураГосЗакупок, Результат.Номенклатура, Результат.ЦенаГосЗакупок);
	Иначе
		ПараметрыПоиска = Новый Структура("НоменклатураГосЗакупок, ЦенаГосЗакупок", Результат.НоменклатураГосЗакупок, Результат.ЦенаГосЗакупок);
	КонецЕсли;
	
	РезультатПоиска = Объект.ПодобраннаяНоменклатура.НайтиСтроки(ПараметрыПоиска);
	
	Если РезультатПоиска.Количество() = 0 Тогда
		ТекущаяСтрока = Объект.ПодобраннаяНоменклатура.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Результат);
	Иначе
		ТекущаяСтрока = РезультатПоиска[0];
		ТекущаяСтрока.КоличествоГосЗакупок = Результат.КоличествоГосЗакупок + ТекущаяСтрока.КоличествоГосЗакупок;
	КонецЕсли;
	
	ТекущаяСтрока.Сумма                = ТекущаяСтрока.КоличествоГосЗакупок * ТекущаяСтрока.ЦенаГосЗакупок;
	
	// Активизируем текущую строку табличной части
	Элементы.ПодобраннаяНоменклатура.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
	
	УправлениеФормой(ЭтотОбъект);
	
	Если ПодборНоменклатуры тогда
		ТекущийЭлемент = Элементы.НоменклатураГосЗакупок;
	Иначе
		ТекущийЭлемент = Элементы.СтрокаПоиска;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПеренестиВДокументЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		ПеренестиВДокумент = Ложь;
		Объект.ПодобраннаяНоменклатура.Очистить();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.ПодобраннаяНоменклатураГруппаГоризонтальноНоменклатураЕдиница.Видимость = Форма.ПодборНоменклатуры;
	Элементы.ПодобраннаяНоменклатураКоэффициент.Видимость = Форма.ПодборНоменклатуры;
	Элементы.НоменклатураГосЗакупокКоличествоОстаток.Видимость = Форма.ПоказыватьОстатки;
	Элементы.НоменклатураГосЗакупокЦена.Видимость = Форма.ПодборНоменклатуры;
	Элементы.ВыбратьОстаток.Видимость = Форма.ПоказыватьОстатки;
	Элементы.ПоказыватьТолькоОстатки.Видимость = Форма.ПоказыватьОстатки;
	Элементы.ПодобраннаяНоменклатураЦена.ТолькоПросмотр = Форма.ПодборНоменклатуры;
	
	ОбновитьИтоги(Форма);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьПодобраннуюНоменклатуруГосЗакупокВХранилище()
	
	ТаблицаНоменклатуры = Объект.ПодобраннаяНоменклатура.Выгрузить();
	ТаблицаНоменклатуры.Колонки.Добавить("КиЗ_ГИСМ", Новый ОписаниеТипов("СправочникСсылка.КонтрольныеЗнакиГИСМ"));
	
	АдресПодобраннойНоменклатурыВХранилище = ПоместитьВоВременноеХранилище(ТаблицаНоменклатуры, УникальныйИдентификатор);
	
	Возврат АдресПодобраннойНоменклатурыВХранилище;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	
	Форма.ИтогиСумма = Форма.Объект.ПодобраннаяНоменклатура.Итог("Сумма");
	Форма.Элементы.ПодобраннаяНоменклатураСумма.ТекстПодвала = Формат(Форма.ИтогиСумма, "ЧЦ=15; ЧДЦ=2");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатураГосЗакупок()
	
	ВыбраннаяНоменклатураГосЗакупок = ПредопределенноеЗначение("Справочник.НоменклатураГосЗакупок.ПустаяСсылка");
	ВыбраннаяЦена = 0;
	Если Элементы.НоменклатураГосЗакупок.ВыделенныеСтроки.Количество() > 0 тогда
		ДанныеПервойВыделеннойСтроки = Объект.НоменклатураГосЗакупок.НайтиПоИдентификатору(Элементы.НоменклатураГосЗакупок.ВыделенныеСтроки.Получить(0));
		ВыбраннаяНоменклатураГосЗакупок = ДанныеПервойВыделеннойСтроки.НоменклатураГосЗакупок;
		ВыбраннаяЦена = ДанныеПервойВыделеннойСтроки.ЦенаГосЗакупок;
	КонецЕсли;
	
	ПараметрыТаблицыНоменклатурыГосЗакупок = Новый Структура;
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ДатаОстатков", ДатаРасчетов);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ПоказатьЦены", Истина);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ПоказатьОстатки", ПодборНоменклатуры);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ОтборПоОстаткам", ПоказыватьТолькоОстатки);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ТипНоменклатуры", "НоменклатураГосЗакупок");
	
	ТаблицаНоменклатурыГосЗакупок = ГосЗакупкиСервер.ТаблицаНоменклатурыГосЗакупок(ПараметрыТаблицыНоменклатурыГосЗакупок);
	Если ТаблицаНоменклатурыГосЗакупок = Неопределено тогда
		Объект.НоменклатураГосЗакупок.Очистить();
		Возврат;
	КонецЕсли;
	
	Если ПодборНоменклатуры тогда
		ТаблицаНоменклатурыГосЗакупок.Сортировать("ЕстьОстаток УБЫВ, Наименование, КоличествоОстаток УБЫВ");
	КонецЕсли;
	
	Объект.НоменклатураГосЗакупок.Загрузить(ТаблицаНоменклатурыГосЗакупок);
	
	Если ЗначениеЗаполнено(ВыбраннаяНоменклатураГосЗакупок) тогда
		Отборы = Новый Структура;
		Отборы.Вставить("НоменклатураГосЗакупок", ВыбраннаяНоменклатураГосЗакупок);
		Отборы.Вставить("ЦенаГосЗакупок", ВыбраннаяЦена);
		
		НайденыеСтроки = Объект.НоменклатураГосЗакупок.НайтиСтроки(Отборы);
		
		Если НайденыеСтроки.Количество() > 0 тогда
			ПерваяНайденаяСтрока = НайденыеСтроки.Получить(0);
			Элементы.НоменклатураГосЗакупок.ТекущаяСтрока = ПерваяНайденаяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодобраннаяНоменклатураВыбраннымиСтрокамиИзСписокНоменклатуры(ЗаполнитьОстатками)
	
	Для Каждого ИдентификаторСтроки Из Элементы.НоменклатураГосЗакупок.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Объект.НоменклатураГосЗакупок.НайтиПоИдентификатору(ИдентификаторСтроки);
		ПараметрыВыбора = ПараметрыВыбораНоменклатурыГосЗакупок(ДанныеСтроки, ЗаполнитьОстатками);
		ДобавитьВыбраннуюНоменклатуруГосЗакупокЗавершение(ПараметрыВыбора, Неопределено);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДополнитьПараметрыВыбораНоменклатурыГосЗакупокНоменклатурой(ПараметрыДляТаблицыГосЗакупок, ПараметрыВыбора)
	
	ТаблицаНоменклатурыГосЗакупок = ГосЗакупкиСервер.ТаблицаНоменклатурыГосЗакупок(ПараметрыДляТаблицыГосЗакупок);
	
	Если ТаблицаНоменклатурыГосЗакупок = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНоменклатурыГосЗакупок.Сортировать("КоличествоОстаток УБЫВ, Наименование");
	
	Если ТаблицаНоменклатурыГосЗакупок = Неопределено тогда
		ПараметрыВыбора.Вставить("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	Иначе
		ПерваяСтрока = ТаблицаНоменклатурыГосЗакупок.Получить(0);
		ПараметрыВыбора.Вставить("Номенклатура"         , ПерваяСтрока.Номенклатура);
		ПараметрыВыбора.Вставить("КоэффициентГосЗакупок", ПерваяСтрока.КоэффициентГосЗакупок);
		ПараметрыВыбора.Вставить("ЕдиницаИзмерения"     , ПерваяСтрока.ЕдиницаИзмерения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыВыбораНоменклатурыГосЗакупок(ДанныеСтроки, ЗаполнитьОстатками, ЗаполнитьНоменклатуру = Истина)
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("НоменклатураГосЗакупок"    , ДанныеСтроки.НоменклатураГосЗакупок);
	Результат.Вставить("КоличествоГосЗакупок"      , 1);
	Результат.Вставить("Коэффициент"               , 1);
	Результат.Вставить("ЦенаГосЗакупок"            , ДанныеСтроки.ЦенаГосЗакупок);
	Результат.Вставить("ЕдиницаИзмеренияГосЗакупок", ДанныеСтроки.ЕдиницаИзмеренияГосЗакупок);
	Результат.Вставить("ДанныеТабличнойЧасти"      , ДанныеТабличнойЧасти);
	
	Если ЗаполнитьНоменклатуру тогда
		ПараметрыДляТаблицыГосЗакупок = Новый Структура;
		ПараметрыДляТаблицыГосЗакупок.Вставить("ДатаОстатков", ДатаРасчетов);
		ПараметрыДляТаблицыГосЗакупок.Вставить("ПоказатьЦены", Ложь);
		ПараметрыДляТаблицыГосЗакупок.Вставить("ПоказатьОстатки", Истина);
		ПараметрыДляТаблицыГосЗакупок.Вставить("ОтборПоОстаткам", Ложь);
		ПараметрыДляТаблицыГосЗакупок.Вставить("НоменклатураГосЗакупок", Результат.НоменклатураГосЗакупок);
		ПараметрыДляТаблицыГосЗакупок.Вставить("Организация", Организация);
		ПараметрыДляТаблицыГосЗакупок.Вставить("Склад", Склад);
		ПараметрыДляТаблицыГосЗакупок.Вставить("ТипНоменклатуры", "Номенклатура");
		
		ДополнитьПараметрыВыбораНоменклатурыГосЗакупокНоменклатурой(ПараметрыДляТаблицыГосЗакупок, Результат);
	КонецЕсли;
	
	Если ЗаполнитьОстатками Тогда
		Результат.КоличествоГосЗакупок = ДанныеСтроки.КоличествоОстаток;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция СписокВыбораНоменклатурыИлиНоменклатурыГосЗакупокНаКлиенте(ПередаваемыеПараметры) Экспорт
	
	Возврат СписокВыбораНоменклатурыИлиНоменклатурыГосЗакупокНаСервере(ПередаваемыеПараметры);
	
КонецФункции

&НаСервере
Функция СписокВыбораНоменклатурыИлиНоменклатурыГосЗакупокНаСервере(ПередаваемыеПараметры)
	
	Возврат ГосЗакупкиСервер.СписокВыбораНоменклатурыИлиНоменклатурыГосЗакупок(ПередаваемыеПараметры);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодобраннаяНоменклатура

&НаКлиенте
Процедура ПодобраннаяНоменклатураПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобраннаяНоменклатураКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПодобраннаяНоменклатура.ТекущиеДанные;
	ТекущиеДанные.Сумма = ТекущиеДанные.ЦенаГосЗакупок * ТекущиеДанные.КоличествоГосЗакупок;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобраннаяНоменклатураЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПодобраннаяНоменклатура.ТекущиеДанные;
	ТекущиеДанные.Сумма = ТекущиеДанные.ЦенаГосЗакупок * ТекущиеДанные.КоличествоГосЗакупок;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобраннаяНоменклатураСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПодобраннаяНоменклатура.ТекущиеДанные;
	
	Если ТекущиеДанные.ЦенаГосЗакупок = 0 тогда
		ТекущиеДанные.КоличествоГосЗакупок = 0;
	Иначе
		ТекущиеДанные.КоличествоГосЗакупок = ТекущиеДанные.Сумма / ТекущиеДанные.ЦенаГосЗакупок;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокНоменклатуры

&НаКлиенте
Процедура НоменклатураГосЗакупокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбратьНоменклатураГосЗакупок(Команды.Найти("ВыбратьСтроку"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНоменклатураГосЗакупок(Команда)
	
	Если Элементы.НоменклатураГосЗакупок.ВыделенныеСтроки.Количество() = 1 Тогда
		
		ДанныеНоменклатуры = Элементы.НоменклатураГосЗакупок.ТекущиеДанные;
		ПараметрыВыбора = ПараметрыВыбораНоменклатурыГосЗакупок(ДанныеНоменклатуры, Команда.Имя = "ВыбратьОстаток", ПодборНоменклатуры);
		
		Если НЕ ПараметрыВыбора = Неопределено тогда
			ОткрытьФормуВводаДополнительныхДанных(ПараметрыВыбора);
		КонецЕсли;
		
	Иначе
		
		ЗаполнитьПодобраннаяНоменклатураВыбраннымиСтрокамиИзСписокНоменклатуры(Команда.Имя = "ВыбратьОстаток");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобраннаяНоменклатураНоменклатураГосЗакупокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ПодборНоменклатуры тогда
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элементы.ПодобраннаяНоменклатура.ТекущиеДанные;
		ГосЗакупкиСервер.ПолеНоменклатураИлиНоменклатураГосЗакупокОбработкаВыбора(ЭтаФорма, ТекущиеДанные, "НоменклатураГосЗакупок", ВыбранноеЗначение);
		ТекущиеДанные.Сумма = ТекущиеДанные.ЦенаГосЗакупок * ТекущиеДанные.КоличествоГосЗакупок;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобраннаяНоменклатураПриАктивизацииСтроки(Элемент)
	
	Если ПодборНоменклатуры тогда
		ГосЗакупкиКлиент.ЗаполнитьСписокВыбораНоменклатурыТаблицыТовары("ПодобраннаяНоменклатураНоменклатура", Элементы.ПодобраннаяНоменклатура.ТекущиеДанные, ЭтаФорма, , ДатаРасчетов);
	КонецЕсли;
	
	ПодобраннаяНоменклатураПриАктивизацииСтрокиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПодобраннаяНоменклатураПриАктивизацииСтрокиНаСервере()
	
	Если Элементы.ПодобраннаяНоменклатура.ТекущаяСтрока = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСтроки = Элементы.ПодобраннаяНоменклатура.ТекущаяСтрока;
	Строка = Объект.ПодобраннаяНоменклатура.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если Строка.ЕдиницаИзмеренияГосЗакупок.Дробная тогда
		Элементы.ПодобраннаяНоменклатураКоличествоГосЗакупок.ФорматРедактирования = "ЧЦ=15; ЧДЦ=3";
	Иначе
		Элементы.ПодобраннаяНоменклатураКоличествоГосЗакупок.ФорматРедактирования = "ЧЦ=15; ЧДЦ=0";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобраннаяНоменклатураНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ПодборНоменклатуры тогда
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элементы.ПодобраннаяНоменклатура.ТекущиеДанные;
		ГосЗакупкиСервер.ПолеНоменклатураИлиНоменклатураГосЗакупокОбработкаВыбора(ЭтаФорма, ТекущиеДанные, "Номенклатура", ВыбранноеЗначение);
		ТекущиеДанные.Сумма = ТекущиеДанные.ЦенаГосЗакупок * ТекущиеДанные.КоличествоГосЗакупок;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти