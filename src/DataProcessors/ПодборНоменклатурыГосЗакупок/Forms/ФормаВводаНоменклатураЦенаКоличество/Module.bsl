&НаКлиенте
Перем НоменклатураВыбрана;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСвойств = "ДатаРасчетов, НоменклатураГосЗакупок, ЕдиницаИзмеренияГосЗакупок,
	                |КоличествоГосЗакупок, ЦенаГосЗакупок, Валюта, Организация, Склад";
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	Если НЕ ЗначениеЗаполнено(НоменклатураГосЗакупок) тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Номенклатура") тогда
		Номенклатура = Параметры.Номенклатура;
	КонецЕсли;
	
	Если Параметры.Свойство("ЕдиницаИзмерения") тогда
		ЕдиницаИзмерения = Параметры.ЕдиницаИзмерения;
	Иначе
		СвойстваНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоменклатураГосЗакупок, "ЕдиницаИзмеренияГосЗакупок");
		
		Заголовок = СвойстваНоменклатуры.Наименование;
		Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
			ЕдиницаИзмерения = СвойстваНоменклатуры.ЕдиницаИзмерения;
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоГосЗакупок = 1 тогда
		КоличествоГосЗакупок = 0;
	КонецЕсли;
	
	Сумма = ЦенаГосЗакупок * КоличествоГосЗакупок;
	
	ВидСтавкиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоменклатураГосЗакупок, "ВидСтавкиНДС");
	СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(ВидСтавкиНДС, ТекущаяДата());
	
	Если ЗначениеЗаполнено(Номенклатура) тогда
		ТекущийЭлемент = Элементы.ТаблицаНоменклатуры;
	КонецЕсли;
	
	Если ЕдиницаИзмеренияГосЗакупок.Дробная тогда
		Элементы.КоличествоГосЗакупок.ФорматРедактирования = "ЧЦ=15; ЧДЦ=3";
	Иначе
		Элементы.КоличествоГосЗакупок.ФорматРедактирования = "ЧЦ=15; ЧДЦ=0";
	КонецЕсли;
	
	ЗаполнитьТаблицуНоменклатура();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НоменклатураВыбрана = Ложь;
	БыстрыйВвод = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	Сумма = ЦенаГосЗакупок * КоличествоГосЗакупок;
	
	Если БыстрыйВвод И ИспользоватьБыстрыйВвод И НоменклатураВыбрана тогда
		ОК(Команды.Найти("ОК"));
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаСервере
Процедура ЗаполнитьТаблицуНоменклатура()

	ВыбраннаяНоменклатура = Номенклатура;
	Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	
	ПараметрыТаблицыНоменклатурыГосЗакупок = Новый Структура;
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ДатаОстатков", ДатаРасчетов);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ПоказатьЦены", Ложь);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ПоказатьОстатки", Истина);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ОтборПоОстаткам", ПоказыватьТолькоОстатки);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("НоменклатураГосЗакупок", НоменклатураГосЗакупок);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("Организация", Организация);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("Склад", Склад);
	ПараметрыТаблицыНоменклатурыГосЗакупок.Вставить("ТипНоменклатуры", "Номенклатура");
	
	ВремТаблицаНоменклатуры = ГосЗакупкиСервер.ТаблицаНоменклатурыГосЗакупок(ПараметрыТаблицыНоменклатурыГосЗакупок);
	Если ВремТаблицаНоменклатуры = Неопределено тогда
		ТаблицаНоменклатуры.Очистить();
		Возврат;
	КонецЕсли;
	
	ВремТаблицаНоменклатуры.Сортировать("ЕстьОстаток УБЫВ, Наименование, КоличествоОстаток УБЫВ");
	
	ТаблицаНоменклатуры.Загрузить(ВремТаблицаНоменклатуры);
	
	Если ЗначениеЗаполнено(ВыбраннаяНоменклатура) тогда
		Отборы = Новый Структура;
		Отборы.Вставить("Номенклатура", Номенклатура);
		
		НайденыеСтроки = ТаблицаНоменклатуры.НайтиСтроки(Отборы);
		
		Если НайденыеСтроки.Количество() > 0 тогда
			ПерваяНайденаяСтрока = НайденыеСтроки.Получить(0);
			Элементы.ТаблицаНоменклатуры.ТекущаяСтрока = ПерваяНайденаяСтрока.ПолучитьИдентификатор();
			Номенклатура = ПерваяНайденаяСтрока.Номенклатура;
		КонецЕсли;
		
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СброситьБыстрыйВыбор()
	
	БыстрыйВвод = Ложь;
	ЭтаФорма.ПоведениеКлавишиEnter = ТипПоведенияКлавишиEnter.ПереходПоЭлементамФормы;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если НЕ ЗначениеЗаполнено(КоличествоГосЗакупок) ИЛИ НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		СброситьБыстрыйВыбор();
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура("НоменклатураГосЗакупок, ЕдиницаИзмеренияГосЗакупок,
	                                    |Номенклатура, ЕдиницаИзмерения, Склад,
										|КоличествоГосЗакупок, КоэффициентГосЗакупок,
										|ЦенаГосЗакупок, Валюта, СтавкаНДС, ДатаРасчетов");
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗакрытия, ЭтотОбъект);	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ТаблицаНоменклатуры.ТекущиеДанные = Неопределено тогда
		Возврат
	КонецЕсли;
	
	Элементы.ОК.Доступность = НЕ Элементы.ТаблицаНоменклатуры.ТекущаяСтрока = Неопределено;
	КоэффициентГосЗакупок = Элементы.ТаблицаНоменклатуры.ТекущиеДанные.КоэффициентГосЗакупок;
	Номенклатура = Элементы.ТаблицаНоменклатуры.ТекущиеДанные.Номенклатура;
	
	Если НоменклатураВыбрана тогда
		СброситьБыстрыйВыбор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СброситьБыстрыйВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КоличествоГосЗакупок = 1;
	СброситьБыстрыйВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СброситьБыстрыйВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыПриАктивизацииПоля(Элемент)
	
	Если НоменклатураВыбрана тогда
		СброситьБыстрыйВыбор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоОстаткиПриИзменении(Элемент)
	
	ЗаполнитьТаблицуНоменклатура();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущийЭлемент = Элементы.КоличествоГосЗакупок;
	НоменклатураВыбрана = Истина;
	
КонецПроцедуры


