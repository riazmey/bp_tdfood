
&ИзменениеИКонтроль("ЗаполнитьТабличныйДокументТН")
Процедура Базар_ЗаполнитьТабличныйДокументТН(ТабличныйДокумент, ТаблицаДанныхДляПечати, ОбъектыПечати, ИмяМакета, ПервыйДокумент)

	КоличествоТН = 0;
	КоличествоТН1208 = 0;
	КоличествоТН1529 = 0;
	КоличествоТН2200 = 0;

	Для Каждого ДанныеПечати Из ТаблицаДанныхДляПечати Цикл

		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ПервыйДокумент = Ложь;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		Если ДанныеПечати.Дата < '20120313' Тогда
			ВариантПечатиНакладной = "ТранспортнаяНакладная";
		ИначеЕсли ДанныеПечати.Дата < '20210101' Тогда
			ВариантПечатиНакладной = "ТранспортнаяНакладная1208";
		Иначе
			ВариантПечатиНакладной = "ТранспортнаяНакладная2200";
		КонецЕсли; 

		Если ВариантПечатиНакладной = "ТранспортнаяНакладная" Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная");
			КоличествоТН = КоличествоТН + 1;
		ИначеЕсли ДанныеПечати.Дата < '20171222' Тогда 
			Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная1208");
			КоличествоТН1208 = КоличествоТН1208 + 1;
		ИначеЕсли ДанныеПечати.Дата < '20210101' Тогда
			Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная1529");
			КоличествоТН1529 = КоличествоТН1529 + 1;
		Иначе
#Удаление
			Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная2200");
#КонецУдаления
#Вставка
			Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьТранспортнойНакладной.Базар_ПФ_MXL_ТранспортнаяНакладная2200");
#КонецВставки
			КоличествоТН2200 = КоличествоТН2200 + 1;
		КонецЕсли;

		ОбластьМакета = Макет.ПолучитьОбласть("ГоризонтальнаяЛицеваяСторона");
		ОбластьМакетаОборотная = Макет.ПолучитьОбласть("ГоризонтальнаяОборотнаяСторона");

		ПараметрыМакета = Новый Структура;

		СведенияОГрузополучателе  = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ДанныеПечати.Грузополучатель,  ДанныеПечати.Дата);
		СведенияОГрузоотправителе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ДанныеПечати.Грузоотправитель, ДанныеПечати.Дата);
		СведенияОПеревозчике      = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ДанныеПечати.Перевозчик,       ДанныеПечати.Дата);

		ПредставлениеГрузоотправителя = "";
		Грузоотправитель              = "";

		Если СведенияОГрузополучателе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			ПараметрыМакета.Вставить("Пункт2_1", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны"));
		ИначеЕсли ВариантПечатиНакладной = "ТранспортнаяНакладная2200" Тогда
			ПараметрыМакета.Вставить("Пункт2_2", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"ПолноеНаименование,ИНН, ФактическийАдрес,Телефоны"));
		Иначе
			ПараметрыМакета.Вставить("Пункт2_2", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"ПолноеНаименование,ФактическийАдрес,Телефоны"));
		КонецЕсли;

		Если СведенияОГрузоотправителе.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
			"ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны");
			ПараметрыМакета.Вставить("Пункт1_1", ПредставлениеГрузоотправителя);
			Грузоотправитель = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, "ПолноеНаименование");
		ИначеЕсли ВариантПечатиНакладной = "ТранспортнаяНакладная2200" Тогда
			ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
			"ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны");
			ПараметрыМакета.Вставить("Пункт1_2", ПредставлениеГрузоотправителя);
			Грузоотправитель = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, "ПолноеНаименование");
		Иначе
			ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, 
			"ПолноеНаименование,ФактическийАдрес,Телефоны");
			ПараметрыМакета.Вставить("Пункт1_2", ПредставлениеГрузоотправителя);
			Грузоотправитель = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОГрузоотправителе, "ПолноеНаименование");
		КонецЕсли;

		ПараметрыМакета.Вставить("Пункт0_2", Формат(ДанныеПечати.Дата, "ДЛФ=D"));

		ПараметрыМакета.Вставить("Пункт3_1", ДанныеПечати.КраткоеНаименованиеГруза);

		ПараметрыМакета.Вставить("Пункт4_1", ДанныеПечати.СопроводительныеДокументы);

		ПараметрыМакета.Вставить("Пункт6_1", СведенияОГрузоотправителе.ФактическийАдрес);

		Если ВариантПечатиНакладной = "ТранспортнаяНакладная1208" Тогда

			Если ЗначениеЗаполнено(ДанныеПечати.ОтпускПроизвел) Тогда 
				ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ДанныеПечати.Организация, ДанныеПечати.ОтпускПроизвел, ДанныеПечати.Дата);
				ПараметрыМакета.Вставить("Пункт6_6", СокрЛП(СтрШаблон(НСтр("ru = '%1 %2 %3 %4'"), ДанныеФизЛица.Должность, ДанныеФизЛица.Фамилия, ДанныеФизЛица.Имя, ДанныеФизЛица.Отчество)));
			КонецЕсли;

			ПараметрыМакета.Вставить("Пункт6_7", ДанныеПечати.Водитель);
			ПараметрыМакета.Вставить("Пункт7_7", ДанныеПечати.Водитель);
			ПараметрыМакета.Вставить("Пункт9_1", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
		КонецЕсли;

		ПараметрыМакета.Вставить("Пункт7_1", ДанныеПечати.АдресДоставки);

#Вставка
		Если ВариантПечатиНакладной = "ТранспортнаяНакладная2200" Тогда
			
			Тоннаж = 0;
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", ДанныеПечати.Ссылка);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СУММА(ЕСТЬNULL(ТранспортнаяЛогистикаДоставка.Тоннаж, 0)) КАК Тоннаж
			|ИЗ
			|	РегистрНакопления.ТранспортнаяЛогистикаДоставка КАК ТранспортнаяЛогистикаДоставка
			|ГДЕ
			|	ТранспортнаяЛогистикаДоставка.ДокументДвижения = &Ссылка
			|	И ТранспортнаяЛогистикаДоставка.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)";
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() цикл
				Если ТипЗнч(Выборка.Тоннаж) = Тип("Число") тогда
					Тоннаж = Тоннаж + Выборка.Тоннаж;
				КонецЕсли;
			КонецЦикла;
			
			Если Тоннаж > 0 тогда
				ПараметрыМакета.Вставить("Пункт3_3", "Масса брутто " + Формат(Тоннаж, "ЧЦ=15; ЧДЦ=0") + " кг.");
			КонецЕсли;
			
			ПараметрыМакета.Вставить("Пункт3_5", ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(ДанныеПечати.Ссылка.СуммаДокумента, ДанныеПечати.Ссылка.ВалютаДокумента));
			ПараметрыМакета.Вставить("Пункт4_1", "");
			ПараметрыМакета.Вставить("Пункт4_2", ДанныеПечати.СопроводительныеДокументы);
			
		КонецЕсли;
#КонецВставки
		ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);

		ТабличныйДокумент.Вывести(ОбластьМакета);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();

		ПараметрыМакетаОборотная = Новый Структура;

		Если СведенияОПеревозчике.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда

			ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
			"ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны");
			ПараметрыМакетаОборотная.Вставить("Пункт10_1", ПредставлениеПеревозчика);
			Перевозчик = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, "ПолноеНаименование");
		ИначеЕсли ВариантПечатиНакладной = "ТранспортнаяНакладная2200" Тогда
			ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
			"ПолноеНаименование, ИНН, ФактическийАдрес,Телефоны");
			ПараметрыМакетаОборотная.Вставить("Пункт10_2", ПредставлениеПеревозчика);
			Перевозчик = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, "ПолноеНаименование");
		Иначе
			ПредставлениеПеревозчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, 
			"ПолноеНаименование,ФактическийАдрес,Телефоны");
			ПараметрыМакетаОборотная.Вставить("Пункт10_2", ПредставлениеПеревозчика);
			Перевозчик = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПеревозчике, "ПолноеНаименование");
		КонецЕсли;

		Если ВариантПечатиНакладной = "ТранспортнаяНакладная" Тогда
			ПараметрыМакетаОборотная.Вставить("Пункт10_4", ДанныеПечати.Водитель);
		Иначе
			ПараметрыМакетаОборотная.Вставить("Пункт10_3", ДанныеПечати.Водитель);
		КонецЕсли;

		ПараметрыМакетаОборотная.Вставить("Пункт11_1", ДанныеПечати.МаркаАвтомобиля);
		ПараметрыМакетаОборотная.Вставить("Пункт11_2", ДанныеПечати.РегистрационныйЗнакАвтомобиля);

		ПараметрыМакетаОборотная.Вставить("Пункт16_1", Грузоотправитель);
		ПараметрыМакетаОборотная.Вставить("Пункт16_2", Перевозчик);

		Если ВариантПечатиНакладной <> "ТранспортнаяНакладная"  Тогда
			ПараметрыМакетаОборотная.Вставить("Пункт16_11", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
			ПараметрыМакетаОборотная.Вставить("Пункт16_21", Формат(ДанныеПечати.Дата, "ДЛФ=D"));
		КонецЕсли;

#Вставка
		Если ВариантПечатиНакладной = "ТранспортнаяНакладная2200" Тогда
			
			ТипВладения = 1;
			
			Если ЗначениеЗаполнено(ДанныеПечати.Автомобиль) тогда
				Если ДанныеПечати.Автомобиль.ТипВладения = Перечисления.Базар_ТипВладенияАвтомобилем.Аренда тогда
					ТипВладения = 2;
				ИначеЕсли ДанныеПечати.Автомобиль.ТипВладения = Перечисления.Базар_ТипВладенияАвтомобилем.Лизинг тогда
					ТипВладения = 3;
				КонецЕсли;
			КонецЕсли;
			
			ПараметрыМакетаОборотная.Вставить("ТипВладения", Строка(ТипВладения));
			
		КонецЕсли;
#КонецВставки
		ОбластьМакетаОборотная.Параметры.Заполнить(ПараметрыМакетаОборотная);

		ТабличныйДокумент.Вывести(ОбластьМакетаОборотная);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
	КонецЦикла;

	Если КоличествоТН = 0 И КоличествоТН1208 = 0 И КоличествоТН1529 = 0 Тогда
		ИмяМакета = "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная2200";
	ИначеЕсли КоличествоТН = 0 И КоличествоТН1208 = 0 И КоличествоТН2200 = 0 Тогда
		ИмяМакета = "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная1529";
	ИначеЕсли КоличествоТН = 0 И КоличествоТН1529 = 0 И КоличествоТН2200 = 0 Тогда
		ИмяМакета = "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная1208";
	ИначеЕсли КоличествоТН1208 = 0 И КоличествоТН1529 = 0 И КоличествоТН2200 = 0 Тогда
		ИмяМакета = "Обработка.ПечатьТранспортнойНакладной.ПФ_MXL_ТранспортнаяНакладная";
	КонецЕсли;

КонецПроцедуры
