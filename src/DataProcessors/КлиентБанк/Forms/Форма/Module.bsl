&НаСервере
Процедура Базар_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Объект.СоздаватьНенайденныеЭлементы = Ложь;
	
КонецПроцедуры

&НаСервере
&Перед("ЗаполнитьДокументыКИмпортуПоРаспознаннымДаннымИзБанка")
Процедура Базар_ЗаполнитьДокументыКИмпортуПоРаспознаннымДаннымИзБанкаПеред(ДокументыКИмпорту, РаспознанныеДанныеИзБанка, ВыводитьСообщения)
	
	КэшВыборокДокументов = Обработки.КлиентБанк.СоздатьКэшВыборокДокументов();
	РаспознанныеДанныеИзБанка.Вставить("КэшВыборокДокументов", КэшВыборокДокументов);
	
КонецПроцедуры

&НаСервере
&После("ЗаполнитьДокументыКИмпортуПоРаспознаннымДаннымИзБанка")
Процедура Базар_ЗаполнитьДокументыКИмпортуПоРаспознаннымДаннымИзБанкаПосле(ДокументыКИмпорту, РаспознанныеДанныеИзБанка, ВыводитьСообщения)
	
	Если РаспознанныеДанныеИзБанка.Свойство("КэшВыборокДокументов") тогда
		РаспознанныеДанныеИзБанка.Удалить("КэшВыборокДокументов");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
&После("ЗаполнитьСтрокуПоХозяйственнойОперации")
Процедура Базар_ЗаполнитьСтрокуПоХозяйственнойОперации(РаспознанныеДанныеИзБанка, СтрокаТаблицы, ХозяйственнаяОперация)
	
	Если РежимАвтораспознаванияДоговоров тогда
		
		КэшВыборокДокументов = Неопределено;
		Если РаспознанныеДанныеИзБанка.Свойство("КэшВыборокДокументов") тогда
			КэшВыборокДокументов = РаспознанныеДанныеИзБанка.КэшВыборокДокументов;
		КонецЕсли; 
		
		СтрокаТаблицы.Договор = Обработки.КлиентБанк.НайтиДоговорКонтрагента(СтрокаТаблицы, Объект.Организация, КэшВыборокДокументов);
		
		Если ХозяйственнаяОперация.Свойство("ДоговорКонтрагента") тогда
			ХозяйственнаяОперация.ДоговорКонтрагента = СтрокаТаблицы.Договор;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&Вместо("ПрочитатьФайлВыпискиНаСервере")
Функция Базар_ПрочитатьФайлВыпискиНаСервере(ОписаниеФайла, Кодировка)
	
	РежимАвтораспознаванияДоговоров = Истина;
	
	Возврат ПродолжитьВызов(ОписаниеФайла, Кодировка);
	
КонецФункции

&После("УправлениеФормойНаСервере")
Процедура Базар_УправлениеФормойНаСервере()
	
	РежимАвтораспознаванияДоговоров = Ложь;
	
КонецПроцедуры

&НаСервере
&Вместо("РазместитьПредупреждение")
Процедура Базар_РазместитьПредупреждение(СтрокаТаблицы, ПредупрежденияРаспознавания = Неопределено, РезультатСозданияДокументов = Неопределено)
	
	МассивПредупреждений = Новый Массив;
	Если ПредупрежденияРаспознавания <> Неопределено Тогда
		Для Каждого Предупреждение Из ПредупрежденияРаспознавания Цикл
			Если Предупреждение.КодПроблемы = "СлучайныйВыбор" Тогда
				МассивПредупреждений.Добавить(Предупреждение.Текст);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если РезультатСозданияДокументов <> Неопределено Тогда
		РезультатСозданияДокумента = РезультатСозданияДокументов[СтрокаТаблицы.ИдентификаторОперации];
		Если РезультатСозданияДокумента <> Неопределено И НЕ РезультатСозданияДокумента.Успешно Тогда
			МассивПредупреждений.Добавить(РезультатСозданияДокумента.КраткоеПредставлениеОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(СтрокаТаблицы.ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийСписаниеДенежныхСредств") Тогда
		ТребуетсяКонтрагент = Документы.СписаниеСРасчетногоСчета.КонтрагентДолженБытьЗаполнен(СтрокаТаблицы.ВидОперации);
	Иначе
		ТребуетсяКонтрагент = Документы.ПоступлениеНаРасчетныйСчет.КонтрагентДолженБытьЗаполнен(СтрокаТаблицы.ВидОперации);
	КонецЕсли;
	
	Если ТребуетсяКонтрагент И НЕ ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) И НЕ ЗначениеЗаполнено(СтрокаТаблицы.КонтрагентСсылкаДляНового) Тогда
		МассивПредупреждений.Добавить(НСтр("ru='Не заполнены реквизиты контрагента'"));
	КонецЕсли;
	
	Если СтрокаТаблицы.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет")
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагента) 
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового) Тогда
		МассивПредупреждений.Добавить(НСтр("ru='Не заполнен счет получателя'"));
	КонецЕсли;
	Если СтрокаТаблицы.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ПереводСДругогоСчета")
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагента) 
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового) Тогда
		МассивПредупреждений.Добавить(НСтр("ru='Не заполнен счет отправителя'"));
	КонецЕсли;
		
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Договор) Тогда
		МассивПредупреждений.Добавить(НСтр("ru='Не заполнен договор'"));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтатьяДДС) тогда
		МассивПредупреждений.Добавить(НСтр("ru='Не установленая статья ДДС'"));
	КонецЕсли;
	
	Если МассивПредупреждений.Количество() > 0 Тогда
		СтрокаТаблицы.ОписаниеОшибок = СтрСоединить(МассивПредупреждений, "; ");
		СтрокаТаблицы.ЕстьОшибка     = Истина;
	КонецЕсли;

	
КонецПроцедуры