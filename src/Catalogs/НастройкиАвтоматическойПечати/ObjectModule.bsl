
Процедура ПередЗаписью(Отказ)
	
	СписокОшибок = Неопределено;
	
	Наименование = ОбъектМетаданных.Наименование;
	
	ПроверкаУникальностиНастроекПечати(СписокОшибок);
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(СписокОшибок, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Наименование");
	
	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПроверкаУникальностиНастроекПечати(СписокОшибок)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиАвтоматическойПечати.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиАвтоматическойПечати КАК НастройкиАвтоматическойПечати
	|ГДЕ
	|	НастройкиАвтоматическойПечати.Ссылка <> &Ссылка
	|	И НастройкиАвтоматическойПечати.ОбъектМетаданных = &ОбъектМетаданных
	|	И НастройкиАвтоматическойПечати.Владелец = &Владелец
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиАвтоматическойПечати.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() цикл
		ТекстСообщения = "Для объекта " +
		                 Строка(ОбъектМетаданных) +
						 " уже существуют настройки автоматической печати.";
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(СписокОшибок,
		                                                        "Объект.ОбъектМетаданных",
		                                                        ТекстСообщения);
	КонецЦикла;
	
КонецПроцедуры