&НаКлиенте
Процедура Базар_ВыбратьВыделеныеПосле(Команда)
	
	ОповеститьОВыборе(ПодготовитьВыбранныеЗначения(Элементы.Список.ТекущиеДанные.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Процедура Базар_СписокВыборПосле(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОповеститьОВыборе(ПодготовитьВыбранныеЗначения(Элементы.Список.ТекущиеДанные.Ссылка));
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьВыбранныеЗначения(ТекущаяСсылка)
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() = 0 ИЛИ НЕ МножественныйВыбор тогда
		
		Возврат ТекущаяСсылка;
		
	Иначе
		
		МассивВыбранных = Новый Массив;
		
		Для Каждого СтрокаСписка из Элементы.Список.ВыделенныеСтроки цикл
			МассивВыбранных.Добавить(СтрокаСписка.Ссылка);
		КонецЦикла;
		
		Возврат МассивВыбранных;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура Базар_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МножественныйВыбор") тогда
		МножественныйВыбор = Параметры.МножественныйВыбор;
	КонецЕСли;
	
	ПараметрыДляЗаполнения = Новый Массив;
	ПараметрыДляЗаполнения.Добавить("ТипЦен");
	ПараметрыДляЗаполнения.Добавить("Склад");
	ПараметрыДляЗаполнения.Добавить("ДатаОстатков");
	ПараметрыДляЗаполнения.Добавить("ДатаЦен");
	
	Для Каждого ИмяПараметра из ПараметрыДляЗаполнения цикл
		Если Параметры.Свойство(ИмяПараметра) тогда
			ЭтаФорма[ИмяПараметра] = Параметры[ИмяПараметра];
		КонецЕсли;
	КонецЦикла;
	
	//ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "ТипЦен, Склад, ДатаОстатков, ДатаЦен");
	
	СистемныеНастройкиБазар = ОбщегоНазначенияБазарСервер.СистемныеНастройкиБазар(ТекущаяДата());
	
	Если НЕ ЗначениеЗаполнено(ТипЦен) тогда
		ТипЦен = СистемныеНастройкиБазар.ТипЦенСебестоимость;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Склад) тогда
		Склад = СистемныеНастройкиБазар.СкладОтгрузки;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаОстатков) тогда
		ДатаОстатков = КонецДня(ТекущаяДата());
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаЦен) тогда
		ДатаЦен = КонецДня(ТекущаяДата());
	КонецЕсли;
	
	ЗаполнитьПараметрыСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура Базар_ПриОткрытииПосле(Отказ)
	
	СформироватьНадписьНастройкиСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура Базар_ОбработкаВыбораПосле(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаНастроекСписка" Тогда
		ОбработкаВыбораНастроекСписка(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Базар_ОткрытьНастройкиСпискаВместо(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Склад"       , Склад);
	ПараметрыФормы.Вставить("ДатаОстатков", ?(ЗначениеЗаполнено(ДатаОстатков), ДатаОстатков, КонецДня(ТекущаяДата())));
	ПараметрыФормы.Вставить("ТипЦен"       , ТипЦен);
	ПараметрыФормы.Вставить("ДатаЦен"     , ?(ЗначениеЗаполнено(ДатаЦен), ДатаЦен, КонецДня(ТекущаяДата())));
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаНастроекСписка", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораНастроекСписка(НастройкиСписка)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, НастройкиСписка);
	СформироватьНадписьНастройкиСписка();
	ЗаполнитьПараметрыСписка();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНадписьНастройкиСписка()
	
	ТекстНадписи = "Склад: ";
	Если ЗначениеЗаполнено(Склад) тогда
		ТекстНадписи = ТекстНадписи + Строка(Склад);
	Иначе
		ТекстНадписи = ТекстНадписи + "<Пусто>";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаОстатков) тогда
		ТекстНадписи = ТекстНадписи + " (" + Формат(ДатаОстатков,"ДФ=dd.MM.yyyy") + ")";
	Иначе
		ТекстНадписи = ТекстНадписи + " (" + Формат(ТекущаяДата(),"ДФ=dd.MM.yyyy") + ")";
	КонецЕсли;
	
	ТекстНадписи = ТекстНадписи + "; Цены: ";
	Если ЗначениеЗаполнено(ТипЦен) тогда
		ТекстНадписи = ТекстНадписи + Строка(ТипЦен);
	Иначе
		ТекстНадписи = ТекстНадписи + "<Пусто>";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаЦен) тогда
		ТекстНадписи = ТекстНадписи + " (" + Формат(ДатаЦен,"ДФ=dd.MM.yyyy") + ")";
	Иначе
		ТекстНадписи = ТекстНадписи + " (" + Формат(ТекущаяДата(),"ДФ=dd.MM.yyyy") + ")";
	КонецЕсли;
	
	Элементы.ДекорацияНастройкиСписка.Заголовок = ТекстНадписи;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыСписка()
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	
	Список.Параметры.УстановитьЗначениеПараметра("Организация" , ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	Список.Параметры.УстановитьЗначениеПараметра("Склад"       , Склад);
	Список.Параметры.УстановитьЗначениеПараметра("ДатаОстатков", ?(ЗначениеЗаполнено(ДатаОстатков), ДатаОстатков, КонецДня(ТекущаяДата())));
	Список.Параметры.УстановитьЗначениеПараметра("ВидыСубконто", ВидыСубконто);
	Список.Параметры.УстановитьЗначениеПараметра("СчетаЗапасов", ГосЗакупкиСервер.МассивСчетовЗапасов(ВидыСубконто));
	Список.Параметры.УстановитьЗначениеПараметра("ТипЦен"      , ТипЦен);
	Список.Параметры.УстановитьЗначениеПараметра("ДатаЦен"     , ?(ЗначениеЗаполнено(ДатаЦен), ДатаЦен, КонецДня(ТекущаяДата())));
	
КонецПроцедуры
