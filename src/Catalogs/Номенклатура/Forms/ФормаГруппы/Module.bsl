&НаСервереБезКонтекста
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ИзменитьУПодчиненныхНоменклатурнуюГруппу(Родитель, НоменклатурнаяГруппа)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", НоменклатурнаяГруппа);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.ЭтоГруппа КАК ЭтоГруппа
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка <> &Родитель
	|	И Номенклатура.Ссылка В ИЕРАРХИИ(&Родитель)
	|	И ВЫБОР
	|			КОГДА Номенклатура.ЭтоГруппа
	|				ТОГДА НЕ Номенклатура.НоменклатурнаяГруппаГруппы = &НоменклатурнаяГруппа
	|			ИНАЧЕ НЕ Номенклатура.НоменклатурнаяГруппа = &НоменклатурнаяГруппа
	|		КОНЕЦ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Если Выборка.ЭтоГруппа тогда
			СправочникОбъект.НоменклатурнаяГруппаГруппы = НоменклатурнаяГруппа;
		Иначе
			СправочникОбъект.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
		КонецЕСли;
		
		СправочникОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросЗаменитьПодчиненныеНоменклатурныеГруппы(Ответ, ПараметрыЗаписи) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПараметрыЗаписи.Вставить("ЗаполнитьПодчиненныеНоменклатурныеГруппы", Истина);
				
	КонецЕсли;
	
	Если ПараметрыЗаписи.Записать тогда
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
	Если ПараметрыЗаписи.Закрыть тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаЗаполненияНоменклатурнойГруппы(ПараметрыЗаписи)
		
	Если ЗначениеРеквизитаОбъекта(Объект.Ссылка, "НоменклатурнаяГруппаГруппы") <> Объект.НоменклатурнаяГруппаГруппы тогда
		ТекстВопроса = НСтр("ru = 'Поле ""Номенклатурная группа"" было изменено.
		                          |Хотите ли Вы изменить это значение у всех подчиненных элементов и групп?'");
		Оповещение = Новый ОписаниеОповещения("ОтветНаВопросЗаменитьПодчиненныеНоменклатурныеГруппы", ЭтотОбъект, ПараметрыЗаписи);
		ПоказатьВопрос(Оповещение,
		               ТекстВопроса,
		               РежимДиалогаВопрос.ДаНет,
					   30,
					   КодВозвратаДиалога.Нет,
					   "Заменить у подчиненных номенклатурные группы?",
		               КодВозвратаДиалога.Нет);
	Иначе
		Если ПараметрыЗаписи.Записать тогда
			Записать(ПараметрыЗаписи);
		КонецЕсли;
		
		Если ПараметрыЗаписи.Закрыть тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросСохранитьИзменения(Ответ, ПараметрыЗаписи) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПараметрыЗаписи.Вставить("Записать", Истина);
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПараметрыЗаписи.Вставить("Записать", Ложь);
		ПараметрыЗаписи.Вставить("Закрыть", Истина);
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	
	КонецЕсли;
	
	Если ПараметрыЗаписи.Записать тогда
		ПроверкаЗаполненияНоменклатурнойГруппы(ПараметрыЗаписи);
	Иначе
		Если ПараметрыЗаписи.Закрыть тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Базар_ПередЗакрытиемВместо(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ПараметрыЗаписи = Новый Структура();
		ПараметрыЗаписи.Вставить("Закрыть", Истина);
		ПараметрыЗаписи.Вставить("Записать", Ложь);
		ПараметрыЗаписи.Вставить("ЗаполнитьПодчиненныеНоменклатурныеГруппы", Ложь);
	
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Вы хотите сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ОтветНаВопросСохранитьИзменения", ЭтотОбъект, ПараметрыЗаписи);
		ПоказатьВопрос(Оповещение,
		               ТекстВопроса,
		               РежимДиалогаВопрос.ДаНетОтмена,
					   30,
					   КодВозвратаДиалога.Да,
					   "Сохранить изменения?",
		               КодВозвратаДиалога.Отмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Базар_ЗаписатьПосле(Команда)
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("Закрыть", Ложь);
	ПараметрыЗаписи.Вставить("Записать", Истина);
	ПараметрыЗаписи.Вставить("ЗаполнитьПодчиненныеНоменклатурныеГруппы", Ложь);
	ПроверкаЗаполненияНоменклатурнойГруппы(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура Базар_ЗаписатьИЗакрытьПосле(Команда)
	
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	ПараметрыЗаписи.Вставить("Записать", Истина);
	ПараметрыЗаписи.Вставить("ЗаполнитьПодчиненныеНоменклатурныеГруппы", Ложь);
	ПроверкаЗаполненияНоменклатурнойГруппы(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура Базар_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.ЗаполнитьПодчиненныеНоменклатурныеГруппы тогда
		ИзменитьУПодчиненныхНоменклатурнуюГруппу(Объект.Ссылка, Объект.НоменклатурнаяГруппаГруппы);
	КонецЕсли;
	
КонецПроцедуры
