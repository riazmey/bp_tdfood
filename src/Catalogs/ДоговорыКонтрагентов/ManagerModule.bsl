Процедура УстановитьОрганизациюПоУмолчанию(ДоговорОбъект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДоговорОбъект.Организация) тогда
		ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		Справочники.Организации.ПроверитьНаличиеОрганизацииПриОднофирменномУчете(ОсновнаяОрганизация);
		ДоговорОбъект.Организация = ОсновнаяОрганизация;
	КонецЕСли;
	
КонецПроцедуры

Процедура УстановитьТипЦенПоУмолчанию(ДоговорОбъект) Экспорт
	
	Если ЗначениеЗаполнено(ДоговорОбъект.ВидДоговора) Тогда
		
		СистемныеНастройкиБазар = ОбщегоНазначенияБазарСервер.СистемныеНастройкиБазар(ТекущаяДата());
		
		Если ДоговорОбъект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем") тогда
			ДоговорОбъект.ТипЦен = СистемныеНастройкиБазар.ТипЦенПродажи;
			
		ИначеЕсли ДоговорОбъект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком") тогда
			ДоговорОбъект.ТипЦен = СистемныеНастройкиБазар.ТипЦенСебестоимость;
			
		Иначе
			ДоговорОбъект.ТипЦен = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция АктуальныеДоговораКонтрагента(Контрагент, Знач Дата = Неопределено, НачальнаяДатаДоговораЭтоДата = Ложь, Знач ВидДоговора = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Дата) тогда
		Дата = Дата(1, 1, 1);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидДоговора) тогда
		ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Контрагент) тогда
		Возврат Новый Массив; 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |ГДЕ
	               |	ВЫБОР
	               |			КОГДА &Дата = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |				ТОГДА ИСТИНА
	               |			ИНАЧЕ КОНЕЦПЕРИОДА(ДоговорыКонтрагентов.СрокДействия, ДЕНЬ) >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	               |		КОНЕЦ
	               |	И ДоговорыКонтрагентов.Владелец = &Контрагент
	               |	И ВЫБОР
	               |			КОГДА &Дата = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |				ТОГДА ИСТИНА
	               |			ИНАЧЕ ВЫБОР
	               |					КОГДА &НачальнаяДатаДоговораЭтоДата
	               |						ТОГДА ВЫБОР
	               |								КОГДА НАЧАЛОПЕРИОДА(ДоговорыКонтрагентов.Дата, ДЕНЬ) > НАЧАЛОПЕРИОДА(ДоговорыКонтрагентов.НачалоДействия, ДЕНЬ)
	               |									ТОГДА НАЧАЛОПЕРИОДА(ДоговорыКонтрагентов.НачалоДействия, ДЕНЬ)
	               |								ИНАЧЕ НАЧАЛОПЕРИОДА(ДоговорыКонтрагентов.Дата, ДЕНЬ)
	               |							КОНЕЦ <= КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
	               |					ИНАЧЕ НАЧАЛОПЕРИОДА(ДоговорыКонтрагентов.НачалоДействия, ДЕНЬ) <= КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
	               |				КОНЕЦ
	               |		КОНЕЦ
	               |	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора";
	
	Запрос.УстановитьПараметр("НачальнаяДатаДоговораЭтоДата", НачальнаяДатаДоговораЭтоДата);
	Запрос.УстановитьПараметр("ВидДоговора", ВидДоговора);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции