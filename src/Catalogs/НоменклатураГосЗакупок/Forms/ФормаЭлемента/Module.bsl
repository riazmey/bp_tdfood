
&НаКлиенте
Процедура НоменклатураНоменклатураПриИзменении(Элемент)
	
	Если НЕ Элементы.Номенклатура.ТекущиеДанные.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка") тогда
		
		ТекущаяНоменклатура = Неопределено;
		Элементы.Номенклатура.ТекущиеДанные.Свойство("Номенклатура", ТекущаяНоменклатура);
		
		Если ЕстьДубликатыНоменклатуры(ТекущаяНоменклатура) тогда
			Элементы.Номенклатура.ТекущиеДанные.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
		Иначе
			Если ВернутьРеквизитОбъекта(ТекущаяНоменклатура, "ВидСтавкиНДС") <> Объект.ВидСтавкиНДС тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Нельзя вводить номенклатуру с отличной ставкой НДС!");
				Элементы.Номенклатура.ТекущиеДанные.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьДоступностьРедактированияКоэффициента();
	
	Если Элементы.Номенклатура.ТекущиеДанные.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка") тогда
		Элементы.Номенклатура.ТекущиеДанные.КоэффициентГосЗакупок = 0;
	Иначе
		Элементы.Номенклатура.ТекущиеДанные.КоэффициентГосЗакупок = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЕстьДубликатыНоменклатуры(ТекущаяНоменклатура)
	
	ОтборыСтрок = Новый Структура;
	ОтборыСтрок.Вставить("Номенклатура", ТекущаяНоменклатура);
	НайденыеСтроки = Объект.Номенклатура.НайтиСтроки(ОтборыСтрок);
	
	Если НайденыеСтроки.Количество() > 1 тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура НоменклатураПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Номенклатура.ТекущаяСтрока = Неопределено тогда
		Элементы.НоменклатураКоэффициент.ТолькоПросмотр = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьДоступностьРедактированияКоэффициента();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьРедактированияКоэффициента()
	
	ТекущаяНоменклатура = Неопределено;
	Элементы.Номенклатура.ТекущиеДанные.Свойство("Номенклатура", ТекущаяНоменклатура);
	
	Если ТекущаяНоменклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка")
	 ИЛИ НЕ ЗначениеЗаполнено(Объект.ЕдиницаИзмеренияГосЗакупок) тогда
		Элементы.НоменклатураКоэффициент.ТолькоПросмотр = Истина;
	Иначе
		Если ВРЕГ(Объект.ЕдиницаИзмеренияГосЗакупок) = "КГ" ИЛИ ВРЕГ(Объект.ЕдиницаИзмеренияГосЗакупок) = "Л" тогда
			Если ВернутьРеквизитОбъекта(ТекущаяНоменклатура, "ЕдиницаИзмерения") = Объект.ЕдиницаИзмеренияГосЗакупок тогда
				Элементы.НоменклатураКоэффициент.ТолькоПросмотр = Истина;
			Иначе
				Элементы.НоменклатураКоэффициент.ТолькоПросмотр = Ложь;
			КонецЕсли;
		Иначе
			Элементы.НоменклатураКоэффициент.ТолькоПросмотр = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьРеквизитОбъекта(Ссылка, Реквизит)
	
	Возврат Ссылка[Реквизит];
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Элементы.СтавкаНДС.РежимВыбораИзСписка = Истина;
	Элементы.СтавкаНДС.СписокВыбора.Очистить();
	ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
	Для Каждого ЗначениеПеречисления Из Перечисления.ВидыСтавокНДС Цикл
		Элементы.СтавкаНДС.СписокВыбора.Добавить(ЗначениеПеречисления,
			Строка(Перечисления.СтавкиНДС.СтавкаНДС(ЗначениеПеречисления, ТекущаяДатаПользователя)));
	КонецЦикла;
	
	НаименованияОдинаковы = Объект.Наименование = Объект.НаименованиеПолное;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.Номенклатура.Доступность = ЗначениеЗаполнено(Объект.ЕдиницаИзмеренияГосЗакупок) И
		                                ЗначениеЗаполнено(Объект.ВидСтавкиНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если НаименованияОдинаковы ИЛИ НЕ ЗначениеЗаполнено(Объект.НаименованиеПолное) тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;
	НаименованияОдинаковы = Объект.НаименованиеПолное = Объект.Наименование;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)
	
	НаименованияОдинаковы = Объект.НаименованиеПолное = Объект.Наименование;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") тогда
		СтандартнаяОбработка = Ложь;
		НоменклатураНоменклатураОбработкаВыбораНаСервере(ВыбранноеЗначение);
	КонецЕсли;
	
	 УстановитьДоступностьРедактированияКоэффициента();
	
КонецПроцедуры

&НаСервере
Процедура НоменклатураНоменклатураОбработкаВыбораНаСервере(МассивЗначений)
	
	ЗаполнилиТекущуюСтроку = Ложь;
	
	Для Каждого Значение из МассивЗначений цикл
		
		Если НЕ ЕстьДубликатыНоменклатуры(Значение) тогда
			
			Если ВернутьРеквизитОбъекта(Значение, "ВидСтавкиНДС") <> Объект.ВидСтавкиНДС тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выбранная номенклатура " + Строка(Значение) + " имеет отличную ставку НДС, от выбранной в текущем элементе!");
				
			Иначе
				
				Если НЕ ЗаполнилиТекущуюСтроку тогда
					СтрокаДляЗаполнения = Объект.Номенклатура.НайтиПоИдентификатору(Элементы.Номенклатура.ТекущаяСтрока);
					ЗаполнилиТекущуюСтроку = Истина;
				Иначе
					СтрокаДляЗаполнения = Объект.Номенклатура.Добавить();
				КонецЕсли;
				
				СтрокаДляЗаполнения.Номенклатура = Значение;
				СтрокаДляЗаполнения.КоэффициентГосЗакупок = 1;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры