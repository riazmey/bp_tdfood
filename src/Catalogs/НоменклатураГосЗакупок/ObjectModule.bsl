
Процедура ПередЗаписью(Отказ)
		
	СписокОшибок = Неопределено;
	
	ПроверкаНаУникальность(СписокОшибок);
	
	Если НЕ ЭтоНовый() тогда
		КонтрольИзмененийВлияющихНаТовароДвижение(СписокОшибок);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(СписокОшибок, Отказ);
	
КонецПроцедуры

Процедура ПроверкаНаУникальность(СписокОшибок)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НоваяТаблицаНоменклатуры", Номенклатура.Выгрузить());
	Запрос.УстановитьПараметр("ТекущийЭлемент", Ссылка);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияГосЗакупок", ЕдиницаИзмеренияГосЗакупок);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(НоваяТаблицаНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(НоваяТаблицаНоменклатуры.НомерСтроки КАК ЧИСЛО(10, 0)) КАК НомерСтроки
	|ПОМЕСТИТЬ ТаблицаНоменклатурыНовая
	|ИЗ
	|	&НоваяТаблицаНоменклатуры КАК НоваяТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатурыНовая.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНоменклатурыНовая.Номенклатура КАК Номенклатура,
	|	НоменклатураГосЗакупокНоменклатура.Ссылка КАК НоменклатураГосЗакупок,
	|	ВЫРАЗИТЬ(&ЕдиницаИзмеренияГосЗакупок КАК Справочник.КлассификаторЕдиницИзмерения) КАК ЕдиницаИзмеренияГосЗакупок
	|ИЗ
	|	ТаблицаНоменклатурыНовая КАК ТаблицаНоменклатурыНовая
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураГосЗакупок.Номенклатура КАК НоменклатураГосЗакупокНоменклатура
	|		ПО ТаблицаНоменклатурыНовая.Номенклатура = НоменклатураГосЗакупокНоменклатура.Номенклатура
	|			И (НоменклатураГосЗакупокНоменклатура.Ссылка <> &ТекущийЭлемент)
	|			И (НоменклатураГосЗакупокНоменклатура.Ссылка.ЕдиницаИзмеренияГосЗакупок = &ЕдиницаИзмеренияГосЗакупок)
	|ГДЕ
	|	НоменклатураГосЗакупокНоменклатура.Ссылка <> &ТекущийЭлемент
	|	И НоменклатураГосЗакупокНоменклатура.Ссылка.ЕдиницаИзмеренияГосЗакупок = &ЕдиницаИзмеренияГосЗакупок
	|	И НоменклатураГосЗакупокНоменклатура.Номенклатура В (ТаблицаНоменклатурыНовая.Номенклатура)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	Номенклатура,
	|	НоменклатураГосЗакупок";
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаНоменклатуры = РезультатЗапроса.Выбрать();
	Пока ВыборкаНоменклатуры.Следующий() цикл
		ТекстОшибки = "Номенклатура """ + Строка(ВыборкаНоменклатуры.Номенклатура) + """, с единицей измерения """ +
		              Строка(ВыборкаНоменклатуры.ЕдиницаИзмеренияГосЗакупок) + """, уже существует в номенклатуре (гос. закупки) """ + 
					  Строка(ВыборкаНоменклатуры.НоменклатураГосЗакупок) + """! Вводить такой же элемент запрещено!";
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(СписокОшибок,
		                                                        "Объект.Номенклатура[%1].Номенклатура",
		                                                        ТекстОшибки,
		                                                        Неопределено,
		                                                        ВыборкаНоменклатуры.НомерСтроки - 1);
	КонецЦикла;
	
КонецПроцедуры

Процедура КонтрольИзмененийВлияющихНаТовароДвижение(СписокОшибок)
	
	ТаблицаДокументов = ГосЗакупкиСервер.ТаблицаДокументовУчаствующихВДвиженияхГосЗакупокСУсловием("НоменклатураГосЗакупок", Ссылка);
		
	Если ТаблицаДокументов.Количество() > 0 И ЕдиницаИзмеренияГосЗакупок <> Ссылка.ЕдиницаИзмеренияГосЗакупок тогда
		ТекстОшибки = "Нельзя изменять едницину измерения номенклатуры (гос. закупки), т.к. она участвует в движении товаров гос. закупок:";
		ГосЗакупкиКлиентСервер.СформироватьТекстСообщенияОписывающихДокументы(ТаблицаДокументов, ТекстОшибки);
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(СписокОшибок,
		                                                        "Объект.ЕдиницаИзмерения",
		                                                        ТекстОшибки,
																Неопределено);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаНаСтарыеДанные", Ссылка);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияГосЗакупок", ЕдиницаИзмеренияГосЗакупок);
	Запрос.УстановитьПараметр("НоваяТаблицаНоменклатуры", Номенклатура.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НоменклатураГосЗакупокНоменклатура.Номенклатура КАК Номенклатура,
	|	НоменклатураГосЗакупокНоменклатура.КоэффициентГосЗакупок КАК КоэффициентГосЗакупок,
	|	НоменклатураГосЗакупокНоменклатура.Ссылка.ЕдиницаИзмеренияГосЗакупок КАК ЕдиницаИзмеренияГосЗакупок
	|ПОМЕСТИТЬ ТаблицаНоменклатурыСтарая
	|ИЗ
	|	Справочник.НоменклатураГосЗакупок.Номенклатура КАК НоменклатураГосЗакупокНоменклатура
	|ГДЕ
	|	НоменклатураГосЗакупокНоменклатура.Ссылка = &СсылкаНаСтарыеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураГосЗакупокНоменклатура.Номенклатура,
	|	НоменклатураГосЗакупокНоменклатура.КоэффициентГосЗакупок,
	|	НоменклатураГосЗакупокНоменклатура.Ссылка.ЕдиницаИзмеренияГосЗакупок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(НоваяТаблицаНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(НоваяТаблицаНоменклатуры.КоэффициентГосЗакупок КАК ЧИСЛО(10, 5)) КАК КоэффициентГосЗакупок,
	|	ВЫРАЗИТЬ(НоваяТаблицаНоменклатуры.НомерСтроки КАК ЧИСЛО(10, 0)) КАК НомерСтроки,
	|	ВЫРАЗИТЬ(&ЕдиницаИзмеренияГосЗакупок КАК Справочник.КлассификаторЕдиницИзмерения) КАК ЕдиницаИзмеренияГосЗакупок
	|ПОМЕСТИТЬ ТаблицаНоменклатурыНовая
	|ИЗ
	|	&НоваяТаблицаНоменклатуры КАК НоваяТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНоменклатурыСтарая.Номенклатура, ТаблицаНоменклатурыНовая.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ТаблицаНоменклатурыНовая.КоэффициентГосЗакупок, 1) КАК КоэффициентНовый,
	|	ЕСТЬNULL(ТаблицаНоменклатурыСтарая.КоэффициентГосЗакупок, 1) КАК КоэффициентСтарый,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаНоменклатурыСтарая.Номенклатура ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ТаблицаНоменклатурыСтарая.КоэффициентГосЗакупок, 1) <> ЕСТЬNULL(ТаблицаНоменклатурыНовая.КоэффициентГосЗакупок, 1)
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоИзменениеКоэффициента,
	|	ТаблицаНоменклатурыНовая.Номенклатура ЕСТЬ NULL
	|		И НЕ ТаблицаНоменклатурыСтарая.Номенклатура ЕСТЬ NULL КАК ЭтоУдалениеНоменклатуры,
	|	ЕСТЬNULL(ТаблицаНоменклатурыНовая.НомерСтроки, 0) КАК НомерСтроки,
	|	ЕСТЬNULL(ТаблицаНоменклатурыСтарая.ЕдиницаИзмеренияГосЗакупок, ТаблицаНоменклатурыНовая.ЕдиницаИзмеренияГосЗакупок) КАК ЕдиницаИзмеренияГосЗакупок
	|ПОМЕСТИТЬ НоменклатураДляПроверки
	|ИЗ
	|	ТаблицаНоменклатурыСтарая КАК ТаблицаНоменклатурыСтарая
	|		ПОЛНОЕ СОЕДИНЕНИЕ ТаблицаНоменклатурыНовая КАК ТаблицаНоменклатурыНовая
	|		ПО ТаблицаНоменклатурыСтарая.Номенклатура = ТаблицаНоменклатурыНовая.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Документ, Базар_ДанныеПервичныхДокументов.Документ) КАК Документ,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, Базар_ДанныеПервичныхДокументов.Дата) КАК Дата,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, Базар_ДанныеПервичныхДокументов.Номер) КАК Номер
	|ПОМЕСТИТЬ ДанныеПервичныхДокументов
	|ИЗ
	|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.Базар_ДанныеПервичныхДокументов КАК Базар_ДанныеПервичныхДокументов
	|		ПО ДанныеПервичныхДокументов.Документ = Базар_ДанныеПервичныхДокументов.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураДляПроверки.Номенклатура КАК Номенклатура,
	|	НоменклатураДляПроверки.ЕдиницаИзмеренияГосЗакупок КАК ЕдиницаИзмеренияГосЗакупок,
	|	НоменклатураДляПроверки.КоэффициентНовый КАК КоэффициентНовый,
	|	НоменклатураДляПроверки.КоэффициентСтарый КАК КоэффициентСтарый,
	|	НоменклатураДляПроверки.ЭтоИзменениеКоэффициента КАК ЭтоИзменениеКоэффициента,
	|	НоменклатураДляПроверки.ЭтоУдалениеНоменклатуры КАК ЭтоУдалениеНоменклатуры,
	|	НоменклатураДляПроверки.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	НоменклатураДляПроверки КАК НоменклатураДляПроверки
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураДляПроверки.Номенклатура,
	|	НоменклатураДляПроверки.ЭтоИзменениеКоэффициента,
	|	НоменклатураДляПроверки.КоэффициентНовый,
	|	НоменклатураДляПроверки.КоэффициентСтарый,
	|	НоменклатураДляПроверки.НомерСтроки,
	|	НоменклатураДляПроверки.ЭтоУдалениеНоменклатуры,
	|	НоменклатураДляПроверки.ЕдиницаИзмеренияГосЗакупок
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыГосЗакупокОбороты.ЕдиницаИзмеренияГосЗакупок КАК ЕдиницаИзмеренияГосЗакупок,
	|	ТоварыГосЗакупокОбороты.Номенклатура КАК Номенклатура,
	|	ТоварыГосЗакупокОбороты.Регистратор КАК Документ,
	|	ДанныеПервичныхДокументов.Дата КАК Дата,
	|	ДанныеПервичныхДокументов.Номер КАК Номер
	|ИЗ
	|	РегистрНакопления.ТоварыГосЗакупок.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					НоменклатураДляПроверки.Номенклатура КАК Номенклатура
	|				ИЗ
	|					НоменклатураДляПроверки КАК НоменклатураДляПроверки)) КАК ТоварыГосЗакупокОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ТоварыГосЗакупокОбороты.Регистратор = ДанныеПервичныхДокументов.Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПервичныхДокументов.Дата,
	|	ДанныеПервичныхДокументов.Номер,
	|	ТоварыГосЗакупокОбороты.Номенклатура,
	|	ТоварыГосЗакупокОбороты.Регистратор,
	|	ТоварыГосЗакупокОбороты.ЕдиницаИзмеренияГосЗакупок
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Документ";
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаОшибок = ПакетРезультатов.Получить(4).Выгрузить();
	ТаблицаДокументов = ПакетРезультатов.Получить(5).Выгрузить();
		
	Если ТаблицаОшибок.Количество() = 0 ИЛИ ТаблицаДокументов.Количество() = 0 тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаОшибки из ТаблицаОшибок цикл
				
		Если СтрокаОшибки.ЭтоИзменениеКоэффициента тогда
			ТекстОшибки = "Нельзя изменять коэффициент пересчета номенклатуры ";
		ИначеЕсли СтрокаОшибки.ЭтоУдалениеНоменклатуры тогда
			ТекстОшибки = "Нельзя удалять номенклатуру ";
		Иначе
			Продолжить;
		КонецЕсли;
		
		ТекстОшибки = ТекстОшибки + " """ + Строка(СтрокаОшибки.Номенклатура) +
		              """, т.к. она участвует в движении товаров гос. закупок:";
		
		Отборы = Новый Структура("Номенклатура, ЕдиницаИзмеренияГосЗакупок", СтрокаОшибки.Номенклатура, ЕдиницаИзмеренияГосЗакупок);
		СтрокиДокументов = ТаблицаДокументов.НайтиСтроки(Отборы);
		
		Если СтрокиДокументов.Количество() > 0 тогда
			ГосЗакупкиКлиентСервер.СформироватьТекстСообщенияОписывающихДокументы(СтрокиДокументов, ТекстОшибки);
			
			Если СтрокаОшибки.ЭтоИзменениеКоэффициента тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(СписокОшибок,
				                                                        "Объект.Номенклатура[%1].Коэффициент",
				                                                        ТекстОшибки,
																		Неопределено,
																		СтрокаОшибки.НомерСтроки - 1);
			ИначеЕсли СтрокаОшибки.ЭтоУдалениеНоменклатуры тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(СписокОшибок,
				                                                        "Объект.Номенклатура",
				                                                        ТекстОшибки,
																		Неопределено);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры