
&НаКлиенте
Процедура Базар_ОтчетДинамикаЦенВместо(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораТипаЦены", ЭтаФорма);
	ПоказатьВыборИзСписка(Оповещение, СписокТиповЦен(), Элементы.ОтчетДинамикаЦен);
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбработкаВыбораТипаЦены(Результат, ПараметрыОповещения) Экспорт
	
	ПараметрыОтчета = ПараметрыОтчетаДинамикаЦенНаСервере();
	
	Если Результат = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыОтчета.ТипЦен = Результат.Значение;
	
	ОткрытьФорму("Отчет.ДинамикаЦен.Форма", ПараметрыОтчета);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыОтчетаДинамикаЦенНаСервере()
	
	СтруктураПараметров = Новый Структура;
	
	СистемныеНастройкиБазар = ОбщегоНазначенияБазарСервер.СистемныеНастройкиБазар(ТекущаяДата());
	
	СтруктураПараметров.Вставить("НачалоПериода"   , НачалоГода(ТекущаяДата()));
	СтруктураПараметров.Вставить("КонецПериода"    , ТекущаяДата());
	СтруктураПараметров.Вставить("Периодичность"   , 9);
	СтруктураПараметров.Вставить("РежимРасшифровки", Истина);
	СтруктураПараметров.Вставить("Номенклатура"    , Параметры.НоменклатураЦен);
	СтруктураПараметров.Вставить("ТипЦен"          , СистемныеНастройкиБазар.ТипЦенСебестоимость);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Функция СписокТиповЦен()
	
	СписокВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Параметры.НоменклатураЦен);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен КАК ТипЦен,
	|	СУММА(ЦеныНоменклатурыСрезПоследних.Цена) КАК Цена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, Номенклатура = &Номенклатура) КАК ЦеныНоменклатурыСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Цена";
	
	Массив = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТипЦен");
	
	Если Массив.Количество() = 0 тогда
		ВыборкаТиповЦен = Справочники.ТипыЦенНоменклатуры.Выбрать();
		
		Пока ВыборкаТиповЦен.Следующий() цикл
			СписокВыбора.Добавить(ВыборкаТиповЦен.Ссылка);
		КонецЦикла;
	Иначе
		СписокВыбора.ЗагрузитьЗначения(Массив);
	КонецЕсли;
	
	Возврат СписокВыбора;
	
КонецФункции
