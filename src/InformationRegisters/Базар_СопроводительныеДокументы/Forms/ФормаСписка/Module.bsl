&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Номенклатура") тогда
		Элементы.GUIDИзображения.Видимость = Ложь;
		Элементы.Номенклатура.Видимость = Ложь;
		Элементы.СтандартНаПродукцию.Видимость = Ложь;
		Список.Параметры.УстановитьЗначениеПараметра("ОтборПоНоменклатуре", Истина);
		Список.Параметры.УстановитьЗначениеПараметра("Номенклатура", Параметры.Отбор.Номенклатура);
	КонецЕсли;
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Документы.СопроводительныйДокумент);
	Элементы.ПодменюСоздать.Видимость = МожноРедактировать;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриОткрытииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Для Каждого Значение из Метаданные.Перечисления.Базар_ВидыСопроводительныхДокументов.ЗначенияПеречисления цикл
		Элемент = Элементы.Найти("ФормаСоздать"+Значение.Имя);
		Если Элемент <> Неопределено тогда
			Элемент.Заголовок = Значение.Синоним;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйДокумент(Команда)
	
	СтрВидОперации = СтрЗаменить(Команда.Имя, "Создать", "");	
	СтруктураПараметров = ПолучитьСтруктуруПараметровФормы(
		ПредопределенноеЗначение("Перечисление.Базар_ВидыСопроводительныхДокументов." + СтрВидОперации));
	ОткрытьФорму("Документ.СопроводительныйДокумент.ФормаОбъекта", СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтруктуруПараметровФормы(ВидОперации)

	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "СозданиеФормыСопроводительныйДокумент");
	
	СтруктураПараметров = Новый Структура;
	
	ЗначенияЗаполнения = ОбщегоНазначенияБПВызовСервера.ЗначенияЗаполненияДинамическогоСписка(Список.КомпоновщикНастроек);
	
	Если ЗначениеЗаполнено(ВидОперации) Тогда
		ЗначенияЗаполнения.Вставить("ВидОперации", ВидОперации);
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ Элементы.Список.ТекущиеДанные = Неопределено тогда
		ПараметрыДляПередачи = Новый Структура;
		ПараметрыДляПередачи.Вставить("Ключ", Элементы.Список.ТекущиеДанные.Регистратор);
		
		ОткрытьФорму("Документ.СопроводительныйДокумент.ФормаОбъекта", ПараметрыДляПередачи);
	КонецЕсли;
	
КонецПроцедуры

