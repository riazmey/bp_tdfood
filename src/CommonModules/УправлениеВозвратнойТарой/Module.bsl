Функция ВернутьТаблицуПередачиТарыВодителю(Дата, Маршрут, Водитель, Автомобиль) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТранспортнаяЛогистикаДоставкаОбороты.Организация КАК Организация,
	               |	НоменклатураВозвратнойТары.ВозвратнаяТара КАК ВозвратнаяТара,
	               |	СУММА(ВЫБОР
	               |			КОГДА НЕ НоменклатураВозвратнойТары.КоличествоВМесте ЕСТЬ NULL
	               |					И НоменклатураВозвратнойТары.КоличествоВМесте > 0
	               |				ТОГДА ТранспортнаяЛогистикаДоставкаОбороты.КоличествоОборот / НоменклатураВозвратнойТары.КоличествоВМесте
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК Количество
	               |ИЗ
	               |	РегистрНакопления.ТранспортнаяЛогистикаДоставка.Обороты(НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ), КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Регистратор, Маршрут = &Маршрут) КАК ТранспортнаяЛогистикаДоставкаОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ВидыТарыНоменклатура.Ссылка.ВозвратнаяТара КАК ВозвратнаяТара,
	               |			ВидыТарыНоменклатура.Номенклатура КАК Номенклатура,
	               |			ВидыТарыНоменклатура.КоличествоВМесте КАК КоличествоВМесте
	               |		ИЗ
	               |			Справочник.ВидыМногооборотнойТары.Номенклатура КАК ВидыТарыНоменклатура) КАК НоменклатураВозвратнойТары
	               |		ПО ТранспортнаяЛогистикаДоставкаОбороты.Номенклатура = НоменклатураВозвратнойТары.Номенклатура
	               |ГДЕ
	               |	НоменклатураВозвратнойТары.ВозвратнаяТара <> ЗНАЧЕНИЕ(Справочник.ВидыМногооборотнойТары.ПустаяСсылка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТранспортнаяЛогистикаДоставкаОбороты.Организация,
	               |	НоменклатураВозвратнойТары.ВозвратнаяТара";
	
	Запрос.УстановитьПараметр("Дата"                         , Дата       );
	Запрос.УстановитьПараметр("Маршрут"                      , Маршрут    );
	Запрос.УстановитьПараметр("Водитель"                     , Водитель   );
	Запрос.УстановитьПараметр("Автомобиль"                   , Автомобиль );
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

Функция ВернутьТаблицуПередачиТарыКонтрагентам(Дата, Маршрут, Водитель, Автомобиль) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТранспортнаяЛогистикаПоДокументам.Документ КАК Документ,
	               |	ТранспортнаяЛогистикаПоДокументам.Контрагент КАК Контрагент
	               |ПОМЕСТИТЬ ЗаказыПокупателей
	               |ИЗ
	               |	РегистрСведений.ТранспортнаяЛогистикаПоДокументам КАК ТранспортнаяЛогистикаПоДокументам
	               |ГДЕ
	               |	ТранспортнаяЛогистикаПоДокументам.Маршрут = &Маршрут
	               |	И ТранспортнаяЛогистикаПоДокументам.Водитель = &Водитель
	               |	И ТранспортнаяЛогистикаПоДокументам.Автомобиль = &Автомобиль
	               |	И ТранспортнаяЛогистикаПоДокументам.Документ ССЫЛКА Документ.ЗаказПокупателя
	               |	И НАЧАЛОПЕРИОДА(ТранспортнаяЛогистикаПоДокументам.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	               |	И ТранспортнаяЛогистикаПоДокументам.Документ.ДоговорКонтрагента.УчитыватьВозвратнуюТаруКонтрагента = ИСТИНА
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТранспортнаяЛогистикаПоДокументам.Документ,
	               |	ТранспортнаяЛогистикаПоДокументам.Контрагент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПродажиОбороты.Организация КАК Организация,
	               |	ВЫБОР
	               |		КОГДА ПродажиОбороты.Регистратор.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	               |			ТОГДА ПродажиОбороты.Контрагент
	               |		ИНАЧЕ ПродажиОбороты.Регистратор.Грузополучатель
	               |	КОНЕЦ КАК Контрагент,
	               |	ПродажиОбороты.ДокументПродажи КАК Документ,
	               |	ПродажиОбороты.Номенклатура КАК Номенклатура,
	               |	ПродажиОбороты.КоличествоОборот КАК Количество
	               |ПОМЕСТИТЬ ПродажиНоменклатуры
	               |ИЗ
	               |	РегистрНакопления.Продажи.Обороты(НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ), КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Регистратор, ) КАК ПродажиОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗаказыПокупателей КАК ЗаказыПокупателей
	               |		ПО ПродажиОбороты.ЗаказПокупателя = ЗаказыПокупателей.Документ
	               |ГДЕ
	               |	ПродажиОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	               |	И ПродажиОбороты.ДоговорКонтрагента.УчитыватьВозвратнуюТаруКонтрагента = ИСТИНА
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПродажиНоменклатуры.Организация КАК Организация,
	               |	ПродажиНоменклатуры.Контрагент КАК Грузополучатель,
	               |	ПродажиНоменклатуры.Документ КАК Документ,
	               |	НоменклатураВозвратнойТары.ВозвратнаяТара КАК ВозвратнаяТара,
	               |	СУММА(ВЫБОР
	               |			КОГДА НЕ НоменклатураВозвратнойТары.КоличествоВМесте ЕСТЬ NULL
	               |					И НоменклатураВозвратнойТары.КоличествоВМесте > 0
	               |				ТОГДА ПродажиНоменклатуры.Количество / НоменклатураВозвратнойТары.КоличествоВМесте
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК Количество
	               |ИЗ
	               |	ПродажиНоменклатуры КАК ПродажиНоменклатуры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ВидыТарыНоменклатура.Ссылка.ВозвратнаяТара КАК ВозвратнаяТара,
	               |			ВидыТарыНоменклатура.Номенклатура КАК Номенклатура,
	               |			ВидыТарыНоменклатура.КоличествоВМесте КАК КоличествоВМесте
	               |		ИЗ
	               |			Справочник.ВидыТары.Номенклатура КАК ВидыТарыНоменклатура) КАК НоменклатураВозвратнойТары
	               |		ПО ПродажиНоменклатуры.Номенклатура = НоменклатураВозвратнойТары.Номенклатура
	               |ГДЕ
	               |	НоменклатураВозвратнойТары.ВозвратнаяТара <> ЗНАЧЕНИЕ(Справочник.ВидыТары.ПустаяСсылка)
	               |	И ВЫБОР
	               |			КОГДА НЕ НоменклатураВозвратнойТары.КоличествоВМесте ЕСТЬ NULL
	               |					И НоменклатураВозвратнойТары.КоличествоВМесте > 0
	               |				ТОГДА ПродажиНоменклатуры.Количество / НоменклатураВозвратнойТары.КоличествоВМесте
	               |			ИНАЧЕ 0
	               |		КОНЕЦ >= 0.45
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПродажиНоменклатуры.Организация,
	               |	НоменклатураВозвратнойТары.ВозвратнаяТара,
	               |	ПродажиНоменклатуры.Контрагент,
	               |	ПродажиНоменклатуры.Документ";
	
	Запрос.УстановитьПараметр("Дата"      , Дата       );
	Запрос.УстановитьПараметр("Маршрут"   , Маршрут    );
	Запрос.УстановитьПараметр("Водитель"  , Водитель   );
	Запрос.УстановитьПараметр("Автомобиль", Автомобиль );
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

Процедура ДополнитьТаблицуТоваровВозвратнойТарой(ДокументСсылка, ТаблицаТовары) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(ВозвратнаяТараДолжникиОбороты.КоличествоОборот) КАК Количество,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара КАК Номенклатура,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.ЕдиницаХраненияОстатков КАК ЕдиницаИзмерения,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.ЕдиницаХраненияОстатков.Коэффициент КАК Коэффициент,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.ЕдиницаХраненияОстатков.Наименование КАК БазоваяЕдиницаНаименование,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код КАК БазоваяЕдиницаКодПоОКЕИ,
	               |	ЛОЖЬ КАК ЕстьСкидкиПоСтроке,
	               |	0 КАК КоличествоМест,
	               |	0 КАК КоличествоВОдномМесте,
	               |	0 КАК МассаБрутто,
	               |	0 КАК Цена,
	               |	0 КАК Сумма,
	               |	0 КАК СуммаНДС,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.Код КАК ТоварКод,
	               |	ВЫРАЗИТЬ(ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.НаименованиеПолное КАК СТРОКА(200)) КАК ТоварНаименование,
	               |	2 КАК Метка,
	               |	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС) КАК СтавкаНДС
	               |ИЗ
	               |	РегистрНакопления.ВозвратнаяТараДолжники.Обороты(НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ), КОНЕЦПЕРИОДА(&Дата, ДЕНЬ), Регистратор, ) КАК ВозвратнаяТараДолжникиОбороты
	               |ГДЕ
	               |	ВозвратнаяТараДолжникиОбороты.РасчетныйДокумент = &ДокументСсылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.ЕдиницаХраненияОстатков,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.ЕдиницаХраненияОстатков.Коэффициент,
	               |	ВЫРАЗИТЬ(ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.НаименованиеПолное КАК СТРОКА(200)),
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.Код,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.ЕдиницаХраненияОстатков.Наименование,
	               |	ВозвратнаяТараДолжникиОбороты.ВозвратнаяТара.ЕдиницаХраненияОстатков.ЕдиницаПоКлассификатору.Код";
	
	Запрос.УстановитьПараметр("Дата"          , ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	СчетчикСтрок = ТаблицаТовары.Количество();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока  = ТаблицаТовары.Добавить();
		СчетчикСтрок = СчетчикСтрок + 1;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.НомерСтроки = СчетчикСтрок;
	КонецЦикла;
	
КонецПроцедуры

Функция ЕстьДвиженияПродажПоДоговору(ДоговорКонтрагента) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПродажиОбороты.Регистратор КАК Регистратор
	               |ИЗ
	               |	РегистрНакопления.Продажи.Обороты(, , Регистратор, ДоговорКонтрагента = &ДоговорКонтрагента) КАК ПродажиОбороты";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество() > 0;
	
КонецФункции

Функция НоменклатураЯвляетсяВозвратнойТарой(Номенклатура) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыТары.Ссылка
	               |ИЗ
	               |	Справочник.ВидыТары КАК ВидыТары
	               |ГДЕ
	               |	ВидыТары.ВозвратнаяТара = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

//&НаСервереБезКонтекста
Процедура ВозвратнаяТараАвтоПодбор(ДанныеВыбора, СтандартнаяОбработка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыТарыНоменклатура.Ссылка.ВозвратнаяТара
	               |ИЗ
	               |	Справочник.ВидыМногооборотнойТары.Номенклатура КАК ВидыТарыНоменклатура
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВидыТарыНоменклатура.Ссылка.ВозвратнаяТара";
	
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВозвратнаяТара"));
	
	Если ДанныеВыбора.Количество() > 0 тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры
