Функция СистемныеНастройкиБазар(РасчетнаяДата) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РасчетнаяДата) тогда
		Возврат Неопределено;
	Иначе
		Если НЕ ТипЗнч(РасчетнаяДата) = Тип("Дата") тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	
	СтруктураВозврата = Новый Структура("ТипЦенСебестоимость,
	                                    |ТипЦенПродажи,
										|ТипЦенРозницы,
	                                    |СкладОтгрузки,
	                                    |ДомашнийРегион,
	                                    |МаксимальнаяСуммаРасчетаНаличкой",
	                                    ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка"),
	                                    ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка"),
	                                    ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"),
	                                    "",
	                                    0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Базар_НастройкиСрезПоследних.ТипЦенСебестоимость КАК ТипЦенСебестоимость,
	               |	Базар_НастройкиСрезПоследних.ТипЦенПродажи КАК ТипЦенПродажи,
	               |	Базар_НастройкиСрезПоследних.ТипЦенРозницы КАК ТипЦенРозницы,
	               |	Базар_НастройкиСрезПоследних.СкладОтгрузки КАК СкладОтгрузки,
	               |	Базар_НастройкиСрезПоследних.ДомашнийРегион КАК ДомашнийРегион,
	               |	Базар_НастройкиСрезПоследних.МаксимальнаяСуммаРасчетаНаличкой КАК МаксимальнаяСуммаРасчетаНаличкой
	               |ИЗ
	               |	РегистрСведений.Базар_Настройки.СрезПоследних(&РасчетнаяДата, ) КАК Базар_НастройкиСрезПоследних";
	
	Запрос.УстановитьПараметр("РасчетнаяДата", РасчетнаяДата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() тогда
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Обработчик подписки на событие ПриЗаписиДокументаРегистрацияДанныхПервичныхДокументовИП
Процедура РегистрацияДанныхПервичныхДокументов(Источник, Отказ) Экспорт
	Перем НомерДокумента, ДатаДокумента;
	
	Если Источник.ОбменДанными.Загрузка = Истина
		И Источник.ДополнительныеСвойства.Свойство("РегистрироватьДанныеПервичныхДокументов")
		И Источник.ДополнительныеСвойства.РегистрироватьДанныеПервичныхДокументов = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Ссылка = Источник.Ссылка;
	ТипДокумента = ТипЗнч(Ссылка);
	
	ИменаРеквизитов = "Организация, Номер, Дата";
	
	МетаданныеДокумента = Источник.Метаданные();

	Если НЕ МетаданныеДокумента.Реквизиты.Найти("НомерВходящегоДокумента") = Неопределено Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", НомерВходящегоДокумента";
		Если НЕ МетаданныеДокумента.Реквизиты.Найти("ДатаВходящегоДокумента") = Неопределено Тогда
			ИменаРеквизитов = ИменаРеквизитов + ", ДатаВходящегоДокумента";
		КонецЕсли;
	КонецЕсли;
	
	Если ТипДокумента = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", ИнвентаризацияТоваровНаСкладе";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", ВидОперации";
	КонецЕсли;
	
	Реквизиты = Новый Структура(ИменаРеквизитов);
	ЗаполнитьЗначенияСвойств(Реквизиты, Источник);
	
	Если НЕ ЗначениеЗаполнено(Реквизиты.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	//Установка управляемой блокировки
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.Базар_ДанныеПервичныхДокументов");
	ЭлементБлокировки.УстановитьЗначение("Документ", Ссылка);
	Блокировка.Заблокировать();
	
	НомерДокумента = "";
	ДатаДокумента  = '00010101';
	
	Если Реквизиты.Свойство("НомерВходящегоДокумента") Тогда
		
		НомерДокумента = СокрЛП(Реквизиты.НомерВходящегоДокумента);
		
		Если Реквизиты.Свойство("ДатаВходящегоДокумента") Тогда
			ДатаДокумента = Реквизиты.ДатаВходящегоДокумента;
		Иначе
			ДатаДокумента = Реквизиты.Дата;
		КонецЕсли;
		
	Иначе
		
		НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер, Истина, Ложь);
		ДатаДокумента  = Реквизиты.Дата;
		
	КонецЕсли;
	
	НаборЗаписейРегистра = РегистрыСведений.Базар_ДанныеПервичныхДокументов.СоздатьНаборЗаписей();
	НаборЗаписейРегистра.Отбор.Документ.Установить(Ссылка);
	
	МенеджерЗаписиРегистра = НаборЗаписейРегистра.Добавить();
	МенеджерЗаписиРегистра.Организация       = Реквизиты.Организация;
	МенеджерЗаписиРегистра.Документ          = Ссылка;
	МенеджерЗаписиРегистра.Номер             = НомерДокумента;
	МенеджерЗаписиРегистра.Дата              = ДатаДокумента;
	МенеджерЗаписиРегистра.НомерРегистратора = Реквизиты.Номер;
	МенеджерЗаписиРегистра.ДатаРегистратора  = Реквизиты.Дата;
	
	// Запись данных первичного документа разрешена всем пользователям,
	// которые вправе изменять этот документ.
	ВыполнитьПроверкуПравДоступа("Изменение", Ссылка.Метаданные());
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписейРегистра.Записать(Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры
