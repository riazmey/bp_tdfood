&Перед("СформироватьДвиженияПоступлениеТоваров")
Процедура Базар_СформироватьДвиженияПоступлениеТоваров(ТаблицаТовары, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаТовары) Тогда
		Возврат;
	КонецЕсли;
	
	ВидНоменклатурыТара = ПредопределенныеЭлементыРасширенияСервер.ПредопределенноеЗначениеРасширения("Справочник", "ВидыНоменклатуры", "МногооборотнаяТара");
	
	Если ЗначениеЗаполнено(ВидНоменклатурыТара) тогда
		
		СчетаМТ = МногооборотнаяТараСервер.СчетаМногооборотнойТары();
		
		Если МногооборотнаяТараСервер.ПредопределенныеСчетаУчетаМногооборотнойТарыЗаполнены(СчетаМТ) тогда
			
			РеквизитыДокумента = ТаблицаРеквизиты.Получить(0);
			КоличествоСтрок = ТаблицаТовары.Количество();
			НомерСтроки = 1;
			Пока НомерСтроки <= КоличествоСтрок цикл
				
				СтрокаТовары = ТаблицаТовары.Получить(НомерСтроки - 1);
				Если СтрокаТовары.Номенклатура.ВидНоменклатуры = ВидНоменклатурыТара тогда
					
					Проводка = Движения.Хозрасчетный.Добавить();
					
					Проводка.Период       = РеквизитыДокумента.Период;
					Проводка.Организация  = РеквизитыДокумента.Организация;
					Проводка.КоличествоДт = СтрокаТовары.Количество;
					Проводка.КоличествоКт = СтрокаТовары.Количество;
					Проводка.Содержание   = СокрЛП(СтрокаТовары.Содержание);
					Проводка.СчетДт       = СчетаМТ.МногооборотнаяТараОстатки;
					Проводка.СчетКт       = СчетаМТ.МногооборотнаяТараПоставщики;
					
					ЗначенияСубконто = Новый Массив;
					ЗначенияСубконто.Добавить(РеквизитыДокумента.Контрагент);
					ЗначенияСубконто.Добавить(СтрокаТовары.Номенклатура);
					ЗначенияСубконто.Добавить(РеквизитыДокумента.Регистратор);
					
					Документы.ДвижениеМногооборотнойТары.ЗаполнитьПроводкуЗначениямиСубконто(
						Проводка.СчетДт,
						Проводка.СубконтоДт,
						ЗначенияСубконто
					);
					
					Документы.ДвижениеМногооборотнойТары.ЗаполнитьПроводкуЗначениямиСубконто(
						Проводка.СчетКт,
						Проводка.СубконтоКт,
						ЗначенияСубконто
					);
					
					ТаблицаТовары.Удалить(НомерСтроки - 1);
					КоличествоСтрок = ТаблицаТовары.Количество();
					НомерСтроки = НомерСтроки - 1;
					
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры