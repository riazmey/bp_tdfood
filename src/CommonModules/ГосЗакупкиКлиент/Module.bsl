Процедура ПолеНоменклатураИлиНоменклатураГосЗакупокОбработкаВыбора(Форма, ДанныеСтроки, ТипНоменклатуры, Знач ДанныеЗаполнения) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ПустаяСтруктура = Новый Структура;
	ПустаяСтруктура.Вставить("НоменклатураГосЗакупок"    , Неопределено);
	ПустаяСтруктура.Вставить("ЕдиницаИзмеренияГосЗакупок", Неопределено);
	ПустаяСтруктура.Вставить("КоэффициентГосЗакупок"     , 1);
	ПустаяСтруктура.Вставить("ЦенаГосЗакупок"            , 0);
	
	Если НЕ ТипЗнч(ДанныеЗаполнения) = Тип("Структура") тогда
		ДанныеЗаполнения = Новый Структура;
		НоменклатураИлиНоменклатураГосЗакупок = ДанныеЗаполнения;
	Иначе
		НоменклатураИлиНоменклатураГосЗакупок = ДанныеЗаполнения[ТипНоменклатуры];
	КонецЕсли;
	
	Для Каждого КлючИЗначение из ПустаяСтруктура цикл
		Если НЕ ДанныеЗаполнения.Свойство(КлючИЗначение.Ключ) тогда
			ДанныеЗаполнения.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	ДанныеЗаполнения.Вставить(ТипНоменклатуры, НоменклатураИлиНоменклатураГосЗакупок);
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения[ТипНоменклатуры]) тогда
		
		ДанныеСтроки[ТипНоменклатуры] = ПредопределенноеЗначение("Справочник." + ТипНоменклатуры + ".ПустаяСсылка");
		Если ТипНоменклатуры = "НоменклатураГосЗакупок" тогда
			ДанныеСтроки.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
		Иначе
			Если НЕ Элементы.Найти("ТоварыНоменклатура") = Неопределено тогда
				Элементы.ТоварыНоменклатура.СписокВыбора.Очистить();
			КонецЕсли;
		КонецЕсли;
		
		ДанныеСтроки.КоэффициентГосЗакупок = 1;
		ДанныеСтроки.ЦенаГосЗакупок = 0;
		
	Иначе
		
		ДанныеСтроки[ТипНоменклатуры] = ДанныеЗаполнения[ТипНоменклатуры];
		
		Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения.КоэффициентГосЗакупок) тогда
			ДанныеЗаполнения.Вставить("КоэффициентГосЗакупок", 1);
		КонецЕсли;
		
		Если ТипНоменклатуры = "НоменклатураГосЗакупок" тогда
			
			ПередаваемыеПараметры = ПараметрыДляПолученияСпискаВыбораНоменклатурыИлиНоменклатурыГосЗакупок("ТоварыНоменклатура", ДанныеСтроки, Форма);
			СписокНоменклатуры = ГосЗакупкиСервер.СписокВыбораНоменклатурыИлиНоменклатурыГосЗакупок(ПередаваемыеПараметры);
			
			Если СписокНоменклатуры.Количество() > 0 тогда
				
				ТекущаяНоменклатураЕстьВСписокНоменклатуры = Ложь;
				Для Каждого ЭлементСписка Из СписокНоменклатуры цикл
					Если ЭлементСписка.Значение.Номенклатура = ДанныеСтроки.Номенклатура тогда
						ТекущаяНоменклатураЕстьВСписокНоменклатуры = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если НЕ ТекущаяНоменклатураЕстьВСписокНоменклатуры тогда
					ДанныеСтроки.Номенклатура = СписокНоменклатуры.Получить(0).Значение.Номенклатура;
					ДанныеЗаполнения.Вставить("КоэффициентГосЗакупок", СписокНоменклатуры.Получить(0).Значение.КоэффициентГосЗакупок);
				Иначе
					ДанныеЗаполнения.Вставить("КоэффициентГосЗакупок", ЭлементСписка.Значение.КоэффициентГосЗакупок);
				КонецЕсли;
				
			Иначе
				ДанныеСтроки.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
				ДанныеЗаполнения.Вставить("КоэффициентГосЗакупок", 1);
			КонецЕсли;
			
		ИначеЕсли ТипНоменклатуры = "Номенклатура" тогда
			
			ПередаваемыеПараметры = ПараметрыДляПолученияСпискаВыбораНоменклатурыИлиНоменклатурыГосЗакупок("НоменклатураГосЗакупок", ДанныеСтроки, Форма);
			ДанныеЦены = ГосЗакупкиСервер.ВернутьЦенуНоменклатурыГосЗаказа(Объект.ДоговорКонтрагента, ДанныеЗаполнения.НоменклатураГосЗакупок, ДанныеЗаполнения.Номенклатура, ПередаваемыеПараметры.ДатаОстатков);
			ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ДанныеЦены);
			
		КонецЕсли;
		
		ГосЗакупкиКлиентСервер.ЗаполнитьДанныеСтрокиТовары(ДанныеСтроки, ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ТоварыНоменклатураПриИзменении(Форма) Экспорт
	
	ТоварыПересчитатьЦеныИКоличествоНоменклатурыГосЗакупок(Форма);
	
КонецПроцедуры

Процедура ТоварыПересчитатьЦеныИКоличествоНоменклатурыГосЗакупок(Форма, ДанныеЦены = Неопределено)
	
	Объект = Форма.Объект;
	
	ДанныеСтроки = Форма.Элементы.Товары.ТекущиеДанные;
	Отборы = Новый Структура("Номенклатура, НомерСтроки", ДанныеСтроки.Номенклатура, ДанныеСтроки.НомерСтроки);
	НайденыеСтроки = Объект.Товары.НайтиСтроки(Отборы);
	
	Если НайденыеСтроки = Неопределено тогда
		Возврат;
	Иначе
		СтрокаТовары = НайденыеСтроки.Получить(0);
	КонецЕсли;
	
	Если ДанныеЦены = Неопределено тогда
		ДанныеЦены = ГосЗакупкиСервер.ВернутьЦенуНоменклатурыГосЗаказа(Объект.ДоговорКонтрагента,
		                                                               СтрокаТовары.НоменклатураГосЗакупок,
		                                                               СтрокаТовары.Номенклатура);
	КонецЕсли;
	
	ГосЗакупкиКлиентСервер.ЗаполнитьДанныеСтрокиТовары(СтрокаТовары, ДанныеЦены);
	ГосЗакупкиКлиентСервер.ТоварыПересчитатьСуммуСуммуНДСиВсего(СтрокаТовары, Объект.СуммаВключаетНДС);
	
	
КонецПроцедуры

Процедура ЗаполнитьСписокВыбораНоменклатурыТаблицыТовары(ИмяПоля, ДанныеСтроки, Форма, СписокВыбора = Неопределено, Знач ДатаОстатков = Неопределено) Экспорт
		
	Элементы = Форма.Элементы;
	Элементы[ИмяПоля].СписокВыбора.Очистить();
	
	Если СписокВыбора = Неопределено тогда
		ПередаваемыеПараметры = ПараметрыДляПолученияСпискаВыбораНоменклатурыИлиНоменклатурыГосЗакупок(ИмяПоля, ДанныеСтроки, Форма, ДатаОстатков);
		СписокВыбора = ГосЗакупкиСервер.СписокВыбораНоменклатурыИлиНоменклатурыГосЗакупок(ПередаваемыеПараметры);
	КонецЕсли;
	
	Если НЕ СписокВыбора = Неопределено тогда
		Для Каждого ЭлементСписка из СписокВыбора цикл
			Элементы[ИмяПоля].СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыДляПолученияСпискаВыбораНоменклатурыИлиНоменклатурыГосЗакупок(ИмяПоля, ДанныеСтроки, Форма, Знач ДатаОстатков = Неопределено) Экспорт
	
	Объект = Форма.Объект;
	
	ТипНоменклатуры = "Номенклатура";
	Если СтрНайти(ИмяПоля, "НоменклатураГосЗакупок") > 0 Тогда
		ТипНоменклатуры = "НоменклатураГосЗакупок";
	КонецЕсли;
	
	ПередаваемыеПараметры = Новый Структура;
	
	Если Объект.Свойство("Ссылка") тогда
		ДатаОстатков = Объект.Дата;
	Иначе
		Если Объект.Свойство("ДатаРасчетов") тогда
			ДатаОстатков = Объект.ДатаРасчетов;
		КонецЕсли;
		Если Объект.Свойство("ДатаОстатков") тогда
			ДатаОстатков = Объект.ДатаОстатков;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаОстатков) тогда
		ДатаОстатков = ТекущаяДата();
	КонецЕсли;
	
	ПередаваемыеПараметры.Вставить("ДатаОстатков", ДатаОстатков);
	ПередаваемыеПараметры.Вставить("ПоказатьЦены", ИмяПоля = "ТоварыНоменклатураГосЗакупок");
	ПередаваемыеПараметры.Вставить("ПоказатьОстатки", Истина);
	ПередаваемыеПараметры.Вставить("ОтборПоОстаткам", Истина);
	
	Если ТипНоменклатуры = "Номенклатура" Тогда
		Если ДанныеСтроки = Неопределено тогда
			Возврат Неопределено;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.НоменклатураГосЗакупок) тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ПередаваемыеПараметры.Вставить("НоменклатураГосЗакупок", ДанныеСтроки.НоменклатураГосЗакупок);
		Если Объект.Свойство("Организация") тогда
			ПередаваемыеПараметры.Вставить("Организация", Объект.Организация);
		Иначе
			ПередаваемыеПараметры.Вставить("Организация", Форма.Организация);
		КонецЕсли;
		ПередаваемыеПараметры.Вставить("ТипНоменклатуры", "Номенклатура");
		
	ИначеЕсли ТипНоменклатуры = "НоменклатураГосЗакупок" Тогда
		Если Объект.Свойство("ДоговорКонтрагента") тогда
			ПередаваемыеПараметры.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
		Иначе
			ПередаваемыеПараметры.Вставить("ДоговорКонтрагента", Форма.ДоговорКонтрагента);
		КонецЕсли;
		ПередаваемыеПараметры.Вставить("ТипНоменклатуры", "НоменклатураГосЗакупок");
		
		
	КонецЕсли;
	
	Возврат ПередаваемыеПараметры;
	
КонецФункции