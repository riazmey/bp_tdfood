Функция ТекстСопроводительныеДокументыНоменклатуры(ДоговорКонтрагента, Номенклатура, Дата, Выборка = Неопределено, ТекстВОднуСтроку = Ложь) Экспорт
	
	Если Выборка = Неопределено тогда
		Если НЕ ЗначениеЗаполнено(ДоговорКонтрагента)
		 ИЛИ НЕ ЗначениеЗаполнено(Номенклатура)
		 ИЛИ НЕ ЗначениеЗаполнено(Дата) тогда
			Возврат "";
		КонецЕсли;
	Иначе
		Выборка.Сбросить();
	КонецЕсли;

	Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура") тогда
		СписокНоменклатуры = Новый Массив();
		СписокНоменклатуры.Добавить(Номенклатура);
	ИначеЕсли ТипЗнч(Номенклатура) = Тип("Массив")
	      ИЛИ ТипЗнч(Номенклатура) = Тип("СписокЗначений") тогда
		СписокНоменклатуры = Номенклатура;
	КонецЕсли;
	
	Если Выборка = Неопределено тогда
		Если ТипЗнч(Номенклатура) = Тип("Массив")
		 ИЛИ ТипЗнч(Номенклатура) = Тип("СписокЗначений") тогда
			Выборка = ВыборкаСопроводительныхДокументов(СписокНоменклатуры, Дата, Ложь);
		Иначе
			Выборка = ВыборкаСопроводительныхДокументов(СписокНоменклатуры, Дата);
		КонецЕсли;
	КонецЕсли;
	
	ТекстСопроводительныхДокументов = "";
	
	СтруктураПоиска = Новый Структура("Номенклатура");
	Если ТипЗнч(Номенклатура) = Тип("Массив")
	 ИЛИ ТипЗнч(Номенклатура) = Тип("СписокЗначений") тогда
		СтруктураПоиска.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	Иначе
		СтруктураПоиска.Номенклатура = Номенклатура;
	КонецЕсли;
	
	Пока Выборка.НайтиСледующий(СтруктураПоиска) Цикл
		Если НЕ ТекстВОднуСтроку тогда
			Если СтрДлина(ТекстСопроводительныхДокументов) > 3 тогда
				ТекстСопроводительныхДокументов = ТекстСопроводительныхДокументов + Символы.ПС;
			КонецЕсли;
		КонецЕсли;
		Если Выборка.ВидОперации = Перечисления.Базар_ВидыСопроводительныхДокументов.ДекларацияСоответствия тогда
			ТекстСопроводительныхДокументов = ТекстСопроводительныхДокументов + "Декл. соотв. №";
		Иначе
			ТекстСопроводительныхДокументов = ТекстСопроводительныхДокументов + "Сертиф. соотв. №";
		КонецЕсли;
		
		ТекстСопроводительныхДокументов = ТекстСопроводительныхДокументов + Выборка.НомерДокумента +
		                                  " от " + Формат(Выборка.ДатаДокумента, "ДФ=dd.MM.yy") + 
		                                  ", действ. до " + Формат(Выборка.ДатаОкончанияДействия, "ДФ=dd.MM.yy") + 
		                                  ?(ГосЗакупкиСервер.ЭтоДоговорПоставкиГосЗакупок(ДоговорКонтрагента),", выдан """ + СокрЛП(Выборка.КомуВыдан) + """" +
		                                  ?(Выборка.ВидОперации = Перечисления.Базар_ВидыСопроводительныхДокументов.СертификатСоответствия,", " + СокрЛП(Выборка.КемВыдан), ""), "");
	КонецЦикла;
	
	Возврат ТекстСопроводительныхДокументов;
	
КонецФункции

Функция ВыборкаСопроводительныхДокументов(МассивНоменклатуры, Дата, ГруппироватьПоНоменклатуре = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА &ГруппироватьПоНоменклатуре
	               |			ТОГДА СопроводительныеДокументыДанные.Номенклатура
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	               |	КОНЕЦ КАК Номенклатура,
	               |	СопроводительныеДокументыДанные.ВидОперации КАК ВидОперации,
	               |	СопроводительныеДокументыДанные.НомерДокумента КАК НомерДокумента,
	               |	СопроводительныеДокументыДанные.Регистратор.Дата КАК ДатаДокумента,
	               |	СопроводительныеДокументыДанные.ДатаОкончанияДействия КАК ДатаОкончанияДействия,
	               |	СопроводительныеДокументыДанные.КемВыдан КАК КемВыдан,
	               |	СопроводительныеДокументыДанные.КомуВыдан КАК КомуВыдан
	               |ИЗ
	               |	РегистрСведений.Базар_СопроводительныеДокументы КАК СопроводительныеДокументыДанные
	               |ГДЕ
	               |	СопроводительныеДокументыДанные.Номенклатура В(&МассивНоменклатуры)
	               |	И КОНЕЦПЕРИОДА(&ДатаДокумента, ДЕНЬ) >= СопроводительныеДокументыДанные.Регистратор.Дата
	               |	И НАЧАЛОПЕРИОДА(&ДатаДокумента, ДЕНЬ) <= СопроводительныеДокументыДанные.ДатаОкончанияДействия
	               |	И (СопроводительныеДокументыДанные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.Базар_ВидыСопроводительныхДокументов.ДекларацияСоответствия)
	               |			ИЛИ СопроводительныеДокументыДанные.ВидОперации = ЗНАЧЕНИЕ(Перечисление.Базар_ВидыСопроводительныхДокументов.СертификатСоответствия))
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопроводительныеДокументыДанные.ВидОперации,
	               |	СопроводительныеДокументыДанные.НомерДокумента,
	               |	СопроводительныеДокументыДанные.Регистратор.Дата,
	               |	СопроводительныеДокументыДанные.ДатаОкончанияДействия,
	               |	СопроводительныеДокументыДанные.КемВыдан,
	               |	СопроводительныеДокументыДанные.КомуВыдан,
	               |	ВЫБОР
	               |		КОГДА &ГруппироватьПоНоменклатуре
	               |			ТОГДА СопроводительныеДокументыДанные.Номенклатура
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	               |	КОНЕЦ";
	
	Запрос.УстановитьПараметр("ГруппироватьПоНоменклатуре", ГруппироватьПоНоменклатуре);
	Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция КраткоеНаименованиеГруза(СписокНоменклатуры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СписокНоменклатуры) тогда
		Возврат "";
	КонецЕсли;
	
	РусскийАлфавит = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ";
	АнглийскийАлфавит = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Родитель.Наименование КАК ГруппаНаименование
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&СписокНоменклатуры)
	|
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура.Родитель.Наименование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппаНаименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() тогда
		Возврат "";
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	СтрокаГруз = "";
	Пока Выборка.Следующий() цикл
		
		ОчищенаяСтрока = "";
		ГруппаНаименование = Выборка.ГруппаНаименование;
		
		ВРЕГСТРОКА = ВРЕГ(ГруппаНаименование);
		КоличествоСимволов = СтрДлина(ВРЕГСТРОКА);
		Для НомерСимвола = 1 по КоличествоСимволов Цикл
			Символ = Сред(ВРЕГСТРОКА, НомерСимвола, 1);
			Если НЕ Найти(РусскийАлфавит, Символ) = 0
			 ИЛИ НЕ Найти(АнглийскийАлфавит, Символ) = 0 тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;

		ОчищенаяСтрока = Прав(ГруппаНаименование, КоличествоСимволов - НомерСимвола + 1);
		
		Если ПустаяСтрока(СтрокаГруз) тогда
			СтрокаГруз = ОчищенаяСтрока;
		Иначе
			СтрокаГруз = СтрокаГруз + ", " + ОчищенаяСтрока;
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаГрузНРЕГ = НРег(СтрокаГруз);
	СтрокаГруз = ВРег(Лев(СтрокаГрузНРЕГ, 1)) + Прав(СтрокаГрузНРЕГ, СтрДлина(СтрокаГрузНРЕГ) - 1);
	
	Возврат СтрокаГруз;
	
КонецФункции