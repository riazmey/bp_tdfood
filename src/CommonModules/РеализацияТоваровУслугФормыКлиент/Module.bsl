&Вместо("ПодборНоменклатуры")
Процедура Базар_ПодборНоменклатуры(Форма, ИмяТаблицы, Команда) Экспорт

	ПараметрыПодбора = ПолучитьПараметрыПодбора(Форма, ИмяТаблицы);
	Если ПараметрыПодбора <> Неопределено Тогда
		Если Форма.ЭтоДокументГосЗакупок тогда
			ОткрытьФорму("Обработка.ПодборНоменклатурыГосЗакупок.Форма.Форма", ПараметрыПодбора,
				Форма, Форма.УникальныйИдентификатор);
		Иначе
			ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
				Форма, Форма.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&Вместо("ПолучитьПараметрыПодбора")
Функция Базар_ПолучитьПараметрыПодбора(Форма, ИмяТаблицы)

	Объект = Форма.Объект;

	ПараметрыФормы = Новый Структура;
	
	ДатаРасчетов 	 = ?(НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДата()), Неопределено, Объект.Дата);
	
	Если Форма.ЭтоДокументГосЗакупок тогда
		ЗаголовокПодбора = НСтр("ru = 'Подбор номенклатуры (гос. закупки) в %1 (%2)'");
	Иначе
		ЗаголовокПодбора = НСтр("ru = 'Подбор номенклатуры в %1 (%2)'");
	КонецЕсли;

	Валюта = Объект.ВалютаДокумента;

	Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		Параметрыформы.Вставить("ПоказыватьЦены", Истина);
	КонецЕсли;

	Если Форма.ЭтоДокументГосЗакупок тогда
		Если ИмяТаблицы = "Товары" Тогда
			ПредставлениеТаблицы = НСтр("ru = 'Товары (гос. закупки)'");
			ПараметрыФормы.Вставить("ПодборНоменклатуры"   , Истина);
			ПараметрыФормы.Вставить("КомандаВыбратьОстаток", Истина);
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Если ИмяТаблицы = "Товары" Тогда
			ПредставлениеТаблицы = НСтр("ru = 'Товары'");
			
			ПараметрыФормы.Вставить("ПоказыватьОстатки"    , Истина);
			ПараметрыФормы.Вставить("КомандаВыбратьОстаток", Истина);
		ИначеЕсли ИмяТаблицы = "ВозвратнаяТара" Тогда
			ПредставлениеТаблицы = НСтр("ru = 'Возвратная тара'");
			
			ПараметрыФормы.Вставить("ПоказыватьОстатки"    , Истина);
			ПараметрыФормы.Вставить("КомандаВыбратьОстаток", Истина);
		ИначеЕсли ИмяТаблицы = "Услуги" Тогда
			ПредставлениеТаблицы = НСтр("ru = 'Услуги'");
		КонецЕсли;
	КонецЕсли;
	ЗаголовокПодбора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ЗаголовокПодбора, Объект.Ссылка, ПредставлениеТаблицы);

	ПараметрыФормы.Вставить("ЕстьЦена"          , Истина);
	ПараметрыФормы.Вставить("ЕстьКоличество"    , Истина);
	ПараметрыФормы.Вставить("ДатаРасчетов"      , ДатаРасчетов);
	ПараметрыФормы.Вставить("ТипЦен"            , Объект.ТипЦен);
	ПараметрыФормы.Вставить("Валюта"            , Валюта);
	ПараметрыФормы.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	ПараметрыФормы.Вставить("Контрагент"        , Объект.Контрагент);
	ПараметрыФормы.Вставить("Организация"       , Объект.Организация);
	ПараметрыФормы.Вставить("Подразделение"     , Объект.ПодразделениеОрганизации);
	ПараметрыФормы.Вставить("Склад"             , Объект.Склад);
	ПараметрыФормы.Вставить("Кратность"         , Объект.КратностьВзаиморасчетов);
	ПараметрыФормы.Вставить("Курс"              , Объект.КурсВзаиморасчетов);
	ПараметрыФормы.Вставить("Заголовок"         , ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВидПодбора"        , "");
	ПараметрыФормы.Вставить("ИмяТаблицы"        , ИмяТаблицы);
	ПараметрыФормы.Вставить("Услуги"            , ИмяТаблицы = "Услуги");
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		ПараметрыФормы.Вставить("ЗаполнятьЦеныПоПродаже", Истина);
		ПараметрыФормы.Вставить("Реализация"            , Истина);
		ПараметрыФормы.Вставить("ДеятельностьНаПатенте" , Объект.ДеятельностьНаПатенте);
		Если ИмяТаблицы = "ВозвратнаяТара" Тогда
			ПараметрыФормы.Вставить("СуммаВключаетНДС"  , Истина);
		Иначе
			ПараметрыФормы.Вставить("СуммаВключаетНДС"  , Объект.СуммаВключаетНДС);
		КонецЕсли;
	КонецЕсли;

	Возврат ПараметрыФормы;

КонецФункции
