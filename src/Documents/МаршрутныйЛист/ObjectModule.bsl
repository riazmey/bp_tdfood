Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ЕстьНезаполненныеИлиНепровоедленныеРеализации = Ложь;
	Для Каждого СтрокаДокумент из ДокументыМаршрута цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаДокумент.ДокументОтгрузки) тогда
			ЕстьНезаполненныеИлиНепровоедленныеРеализации = Истина;
			Прервать;
		КонецЕсли;
		
		Если НЕ СтрокаДокумент.ДокументОтгрузки.Проведен тогда
			ЕстьНезаполненныеИлиНепровоедленныеРеализации = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьНезаполненныеИлиНепровоедленныеРеализации И НЕ ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) тогда
		
		Сообщить("Нельзя проводить документ ""Маршрутный лист"" при не сформированных документах, или при непроведенных Реализациях!");
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Дата",Дата-1);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	МаршрутныйЛистСоставыРейсов.Ссылка.Организация КАК Организация,
	|	МаршрутныйЛистСоставыРейсов.Ссылка.Маршрут КАК Маршрут,
	|	МаршрутныйЛистСоставыРейсов.Рейс КАК Рейс,
	|	МаршрутныйЛистСоставыРейсов.НомерВРейсе КАК НомерВРейсе,
	|	МаршрутныйЛистСоставыРейсов.ТочкаМаршрута КАК ТочкаМаршрута,
	|	МаршрутныйЛистСоставыРейсов.АдресПредставление КАК АдресПредставление,
	|	МаршрутныйЛистСоставыРейсов.ВремяНачала КАК ВремяЛогистикиНачало,
	|	МаршрутныйЛистСоставыРейсов.ВремяОкончания КАК ВремяЛогистикиОкончание,
	|	МаршрутныйЛистСоставыРейсов.GUID КАК GUID
	|ПОМЕСТИТЬ СоставыРейсов
	|ИЗ
	|	Документ.МаршрутныйЛист.СоставыРейсов КАК МаршрутныйЛистСоставыРейсов
	|ГДЕ
	|	МаршрутныйЛистСоставыРейсов.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаршрутныйЛистДокументыМаршрута.ДокументОтгрузки КАК Документ,
	|	ЕСТЬNULL(РеализацияТоваровУслугТовары.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	СУММА(РеализацияТоваровУслугТовары.Номенклатура.ВесЕдиницы * РеализацияТоваровУслугТовары.Количество) КАК Тоннаж,
	|	СУММА(РеализацияТоваровУслугТовары.Количество) КАК Количество,
	|	ЕСТЬNULL(МаршрутныйЛистДокументыМаршрута.GUID, """") КАК GUID
	|ПОМЕСТИТЬ ДанныеРеализации
	|ИЗ
	|	Документ.МаршрутныйЛист.ДокументыМаршрута КАК МаршрутныйЛистДокументыМаршрута
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО МаршрутныйЛистДокументыМаршрута.ДокументОтгрузки = РеализацияТоваровУслугТовары.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка.Проведен = ИСТИНА
	|	И НЕ МаршрутныйЛистДокументыМаршрута.GUID ЕСТЬ NULL
	|	И МаршрутныйЛистДокументыМаршрута.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	МаршрутныйЛистДокументыМаршрута.ДокументОтгрузки,
	|	ЕСТЬNULL(РеализацияТоваровУслугТовары.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
	|	ЕСТЬNULL(МаршрутныйЛистДокументыМаршрута.GUID, """")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставыРейсов.Организация КАК Организация,
	|	СоставыРейсов.Маршрут КАК Маршрут,
	|	СоставыРейсов.Рейс КАК Рейс,
	|	СоставыРейсов.НомерВРейсе КАК НомерВРейсе,
	|	СоставыРейсов.ТочкаМаршрута КАК ТочкаМаршрута,
	|	ДанныеРеализации.Документ КАК ДокументДвижения,
	|	ДанныеРеализации.Номенклатура КАК Номенклатура,
	|	СУММА(ДанныеРеализации.Количество) КАК Количество,
	|	СУММА(ДанныеРеализации.Тоннаж) КАК Тоннаж,
	|	СоставыРейсов.АдресПредставление КАК АдресПредставление,
	|	СоставыРейсов.ВремяЛогистикиНачало КАК ВремяНачала,
	|	СоставыРейсов.ВремяЛогистикиОкончание КАК ВремяОкончания
	|ИЗ
	|	ДанныеРеализации КАК ДанныеРеализации
	|		ПОЛНОЕ СОЕДИНЕНИЕ СоставыРейсов КАК СоставыРейсов
	|		ПО ДанныеРеализации.GUID = СоставыРейсов.GUID
	|ГДЕ
	|	ДанныеРеализации.Документ.Проведен = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	СоставыРейсов.Организация,
	|	СоставыРейсов.Маршрут,
	|	СоставыРейсов.Рейс,
	|	СоставыРейсов.НомерВРейсе,
	|	СоставыРейсов.ТочкаМаршрута,
	|	ДанныеРеализации.Номенклатура,
	|	ДанныеРеализации.Документ,
	|	СоставыРейсов.ВремяЛогистикиНачало,
	|	СоставыРейсов.ВремяЛогистикиОкончание,
	|	СоставыРейсов.АдресПредставление";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() тогда
		
		Выборка = Результат.Выбрать();
		ДвиженияТранспортнаяЛогистикаДоставка = Движения.ТранспортнаяЛогистикаДоставка;
		
		Пока Выборка.Следующий() Цикл
			
			НовоеДвижение = ДвиженияТранспортнаяЛогистикаДоставка.Добавить();
			
			ЗаполнитьЗначенияСвойств(НовоеДвижение, Выборка);
			
			НовоеДвижение.Период = Дата;
			НовоеДвижение.Регистратор = Ссылка;
			НовоеДвижение.ВидДвижения = ВидДвиженияНакопления.Приход;
			НовоеДвижение.Водитель = Водитель;
			НовоеДвижение.Автомобиль = Автомобиль;
			НовоеДвижение.Пробег = Пробег;
			
		КонецЦикла;
		ДвиженияТранспортнаяЛогистикаДоставка.Записать();
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Заполнение даты документа
	ФиксированноеВремяДокументов.УстановитьВремяНовогоДокумента(ЭтотОбъект, Отказ);
	
	ЭтотОбъект.Рейсы.Сортировать("Рейс");
	ЭтотОбъект.СоставыРейсов.Сортировать("НомерВРейсе, ТочкаМаршрута, АдресПредставление, ВремяНачала, ВремяОкончания");
	
КонецПроцедуры
