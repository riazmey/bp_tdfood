
&После("ПередЗаписью")
Процедура Базар_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	
	ДокументОснования = Неопределено;
	Если ДокументыОснования.Количество() = 1 тогда
		ПерваяСтрока = ДокументыОснования.Получить(0);
		
		Если ТипЗнч(ПерваяСтрока.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") тогда
			ДокументОснования = ПерваяСтрока.ДокументОснование;
		КонецЕсли;
	КонецЕсли;
	
	Если ДокументОснования = Неопределено тогда
		
		ПодписиУполномоченныхЛиц = ОтветственныеЛицаБП.РеквизитыПодписиУполномоченныхЛиц(Организация,
		                                                                                 ПользователиКлиентСервер.ТекущийПользователь(),
		                                                                                 Неопределено,
		                                                                                 Дата);
		
		ПодписиРуководитель              = ПодписиУполномоченныхЛиц.Получить(ПредопределенноеЗначение("Перечисление.ОтветственныеЛицаОрганизаций.Руководитель"));
		ПодписиГлавныйБухгалтер          = ПодписиУполномоченныхЛиц.Получить(ПредопределенноеЗначение("Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер"));
		ПодписиОтветственныйЗаОформление = ПодписиУполномоченныхЛиц.Получить(ПредопределенноеЗначение("Перечисление.ОтветственныеЛицаОрганизаций.ОтветственныйЗаОформление"));
		
		Руководитель                     = ПодписиРуководитель.ФизическоеЛицо;
		ЗаРуководителяНаОсновании        = ПодписиРуководитель.ОснованиеПраваПодписи;
		ГлавныйБухгалтер                 = ПодписиГлавныйБухгалтер.ФизическоеЛицо;
		ЗаГлавногоБухгалтераНаОсновании  = ПодписиГлавныйБухгалтер.ОснованиеПраваПодписи;
		ОтветственныйЗаОформление        = ПодписиОтветственныйЗаОформление.ФизическоеЛицо;
		
	Иначе
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		                         ДокументОснования,
		                         "Руководитель,
		                         |ЗаРуководителяНаОсновании,
		                         |ГлавныйБухгалтер,
		                         |ЗаГлавногоБухгалтераНаОсновании,
		                         |ОтветственныйЗаОформление");
		
	КонецЕсли;
	
КонецПроцедуры
