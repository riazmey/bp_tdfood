
&После("ОбработкаПроведения")
Процедура Базар_ОбработкаПроведения(Отказ, РежимПроведения)
	
	СистемныеНастройкиБазар = ОбщегоНазначенияБазарСервер.СистемныеНастройкиБазар(Дата);
	
	Если ЗначениеЗаполнено(СистемныеНастройкиБазар.ТипЦенСебестоимость) тогда
		ЗаполнитьДокументУстановкаЦенНоменклатуры(СистемныеНастройкиБазар.ТипЦенСебестоимость);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СистемныеНастройкиБазар.ТипЦенПродажи) тогда
		ЗаполнитьДокументУстановкаЦенНоменклатуры(СистемныеНастройкиБазар.ТипЦенПродажи);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СистемныеНастройкиБазар.ТипЦенРозницы) тогда
		ЗаполнитьДокументУстановкаЦенНоменклатуры(СистемныеНастройкиБазар.ТипЦенРозницы);
	КонецЕсли;
	
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура Базар_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() тогда
		
		СуммаВключаетНДС = Истина;
		
		Если НЕ ЗначениеЗаполнено(Склад) тогда
			СистемныеНастройкиБазар = ОбщегоНазначенияБазарСервер.СистемныеНастройкиБазар(Дата);
			Если ЗначениеЗаполнено(СистемныеНастройкиБазар.СкладОтгрузки) тогда
				Склад = СистемныеНастройкиБазар.СкладОтгрузки;
			КонецЕсли;
			Если ЗначениеЗаполнено(СистемныеНастройкиБазар.ТипЦенСебестоимость) тогда
				ТипЦен = СистемныеНастройкиБазар.ТипЦенСебестоимость;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументУстановкаЦенНоменклатуры(ТипЦен)
	
	Если НЕ ЗначениеЗаполнено(ТипЦен) тогда
		Возврат;
	КонецЕсли;
	
	УстановкиЦенНоменклатуры = Ценообразование.НайтиУстановкиЦенНоменклатуры(Ссылка, ТипЦен);
	Если УстановкиЦенНоменклатуры = Неопределено тогда
		ОбъектУстановкаЦенНоменклатуры = Документы.УстановкаЦенНоменклатуры.СоздатьДокумент();
	Иначе
		ОбъектУстановкаЦенНоменклатуры = УстановкиЦенНоменклатуры.Получить(0).ПолучитьОбъект();
	КонецЕсли;
	
	ОбъектУстановкаЦенНоменклатуры.Дата                       = Дата;
	ОбъектУстановкаЦенНоменклатуры.ДокументОснование          = Ссылка;
	ОбъектУстановкаЦенНоменклатуры.ТипЦен                     = ТипЦен;
	ОбъектУстановкаЦенНоменклатуры.Ответственный              = Ответственный;
	ОбъектУстановкаЦенНоменклатуры.Комментарий                = "Автоматическое создание документа. Основание: Проведение документа "+Ссылка;
	ОбъектУстановкаЦенНоменклатуры.НеПроводитьНулевыеЗначения = Истина;
	ОбъектУстановкаЦенНоменклатуры.ПометкаУдаления            = Ложь;
	
	ОбъектУстановкаЦенНоменклатуры.Товары.Очистить();
	Для Каждого СтрокаТовары из Товары цикл
		
		НоваяСтрокаТовары = ОбъектУстановкаЦенНоменклатуры.Товары.Добавить();
		НоваяСтрокаТовары.Номенклатура = СтрокаТовары.Номенклатура;
		НоваяСтрокаТовары.Валюта = ВалютаДокумента;
		
		Если ЗначениеЗаполнено(ТипЦен.БазовыйТипЦен) тогда
			ПолученнаяЦена = СтрокаТовары.Цена + (СтрокаТовары.Цена * ТипЦен.ПроцентСкидкиНаценки / 100);
		Иначе
			ПолученнаяЦена = СтрокаТовары.Цена;
		КонецЕсли;
		
		НоваяСтрокаТовары.Цена = Ценообразование.ОкруглитьЦену(ПолученнаяЦена, ТипЦен);
		
	КонецЦикла;
	
	ОбъектУстановкаЦенНоменклатуры.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		
КонецПроцедуры

&После("ОбработкаУдаленияПроведения")
Процедура Базар_ОбработкаУдаленияПроведения(Отказ)
	
	УстановкиЦенНоменклатуры = Ценообразование.НайтиУстановкиЦенНоменклатуры(Ссылка);
	Если УстановкиЦенНоменклатуры = Неопределено тогда
		Возврат;
	Иначе
		Для Каждого СсылкаУстановкаЦенНоменклатуры из УстановкиЦенНоменклатуры цикл
			ОбъектУстановкаЦенНоменклатуры = СсылкаУстановкаЦенНоменклатуры.ПолучитьОбъект();
			ОбъектУстановкаЦенНоменклатуры.Записать(РежимЗаписиДокумента.ОтменаПроведения,
			                                        РежимПроведенияДокумента.Неоперативный);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&После("ПередЗаписью")
Процедура Базар_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Обработка пометки на удаление
	Если ПометкаУдаления <> Ссылка.ПометкаУдаления тогда
		
		УстановкиЦенНоменклатуры = Ценообразование.НайтиУстановкиЦенНоменклатуры(Ссылка);
		Если УстановкиЦенНоменклатуры = Неопределено тогда
			Возврат;
			
		Иначе
			Для Каждого СсылкаУстановкаЦенНоменклатуры из УстановкиЦенНоменклатуры цикл
				
				ОбъектУстановкаЦенНоменклатуры = СсылкаУстановкаЦенНоменклатуры.ПолучитьОбъект();
				ОбъектУстановкаЦенНоменклатуры.ПометкаУдаления = ПометкаУдаления;
				
				Если ПометкаУдаления тогда
					ОбъектУстановкаЦенНоменклатуры.Записать(РежимЗаписиДокумента.ОтменаПроведения,
					                                        РежимПроведенияДокумента.Неоперативный);
				Иначе
					Если Проведен тогда
						ОбъектУстановкаЦенНоменклатуры.Записать(РежимЗаписиДокумента.Проведение,
						                                        РежимПроведенияДокумента.Неоперативный);
					Иначе
						ОбъектУстановкаЦенНоменклатуры.Записать(РежимЗаписиДокумента.Запись,
						                                        РежимПроведенияДокумента.Неоперативный);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
