Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидОперации = ПредопределенноеЗначение("Перечисление.Базар_ВидыСопроводительныхДокументов.КачественноеУдостоверение") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтандартыНаПродукцию");
		МассивНепроверяемыхРеквизитов.Добавить("СтандартыНаПродукцию.СтандартНаПродукцию");
		МассивНепроверяемыхРеквизитов.Добавить("КемВыдан");
		МассивНепроверяемыхРеквизитов.Добавить("КомуВыдан");
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.Базар_ВидыСопроводительныхДокументов.ПротоколЛабораторногоИсследования") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтандартыНаПродукцию");
		МассивНепроверяемыхРеквизитов.Добавить("СтандартыНаПродукцию.СтандартНаПродукцию");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаОкончанияДействия");
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.Базар_ВидыСопроводительныхДокументов.ИнформационноеПисьмо") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтандартыНаПродукцию");
		МассивНепроверяемыхРеквизитов.Добавить("СтандартыНаПродукцию.СтандартНаПродукцию");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаОкончанияДействия");
		МассивНепроверяемыхРеквизитов.Добавить("КемВыдан");
	КонецЕсли;
	
	// Проверка значения реквизита ДатаОкончанияДействия
	Если МассивНепроверяемыхРеквизитов.Найти("ДатаОкончанияДействия") = Неопределено тогда
		Если Дата > ДатаОкончанияДействия Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Срок действия документа не должен быть меньше чем дата документа!'");
			Сообщение.Поле = "ДатаОкончанияДействия";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина; 
			МассивНепроверяемыхРеквизитов.Добавить("ДатаОкончанияДействия"); 
		КонецЕсли;
	КонецЕсли;
	
	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Базар_СопроводительныеДокументы.Записывать = Истина;
	Движения.Базар_СопроводительныеДокументы.Очистить();
	
	Если ВидОперации = Перечисления.Базар_ВидыСопроводительныхДокументов.КачественноеУдостоверение
	 ИЛИ ВидОперации = Перечисления.Базар_ВидыСопроводительныхДокументов.ПротоколЛабораторногоИсследования
	 ИЛИ ВидОперации = Перечисления.Базар_ВидыСопроводительныхДокументов.ИнформационноеПисьмо тогда
		Для Каждого ТекСтрокаНоменклатура Из Номенклатура Цикл
			Для Каждого ТекСтрокаИзображение из Изображения цикл
				ЗаписатьДвижение(ТекСтрокаНоменклатура, ТекСтрокаИзображение);
			КонецЦикла;
		КонецЦикла;
	Иначе
		Для Каждого ТекСтрокаНоменклатура Из Номенклатура Цикл
			Для Каждого ТекСтрокаСтандартыНаПродукцию Из СтандартыНаПродукцию Цикл
				Для Каждого ТекСтрокаИзображение из Изображения цикл
					ЗаписатьДвижение(ТекСтрокаНоменклатура, ТекСтрокаИзображение, ТекСтрокаСтандартыНаПродукцию);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьДвижение(СтрокаНоменклатура, СтрокаИзображение, СтрокаСтандартыНаПродукцию = Неопределено)
	
	Движение = Движения.Базар_СопроводительныеДокументы.Добавить();
	Движение.НомерДокумента        = НомерДокумента;
	Движение.ВидОперации           = ВидОперации;
	Движение.ДатаОкончанияДействия = КонецДня(ДатаОкончанияДействия);
	Движение.Номенклатура          = СтрокаНоменклатура.Номенклатура;
	
	Если ЗначениеЗаполнено(СтрокаСтандартыНаПродукцию) тогда
		Движение.СтандартНаПродукцию   = СтрокаСтандартыНаПродукцию.СтандартНаПродукцию;
	КонецЕсли;
	
	Движение.GUIDИзображения       = СтрокаИзображение.GUIDИзображения;
	Движение.КемВыдан              = КемВыдан;
	Движение.КомуВыдан             = КомуВыдан;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	Изображения.Очистить();
	НомерДокумента = "";
	ДатаОкончанияДействия = "";
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ДанныеЗаполнения.Свойство("Номенклатура") тогда
		НоваяСтрока = Номенклатура.Добавить();
		НоваяСтрока.Номенклатура = ДанныеЗаполнения.Номенклатура;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Дата = НачалоДня(Дата);
	
КонецПроцедуры
