
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Ссылка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТранспортнаяЛогистикаСоставыРейсов.Ссылка.Организация КАК Организация,
	|	ТранспортнаяЛогистикаДокументыМаршрута.Документ КАК ДокументДвижения,
	|	ВЫБОР
	|		КОГДА ТранспортнаяЛогистикаДокументыМаршрута.Документ.Грузоотправитель <> ТранспортнаяЛогистикаДокументыМаршрута.Документ.Организация
	|				И ТранспортнаяЛогистикаДокументыМаршрута.Документ.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ТранспортнаяЛогистикаДокументыМаршрута.Документ.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ТранспортнаяЛогистикаДокументыМаршрута.Документ.Грузоотправитель
	|		ИНАЧЕ ТранспортнаяЛогистикаДокументыМаршрута.Документ.Организация
	|	КОНЕЦ КАК Грузоотправитель,
	|	ТранспортнаяЛогистикаСоставыРейсов.ТочкаМаршрута КАК Грузополучатель,
	|	ТранспортнаяЛогистикаСоставыРейсов.АдресПредставление КАК АдресПредставление,
	|	ТранспортнаяЛогистикаСоставыРейсов.ВремяНачала КАК ВремяЛогистикиНачало,
	|	ТранспортнаяЛогистикаСоставыРейсов.ВремяОкончания КАК ВремяЛогистикиОкончание,
	|	ТранспортнаяЛогистикаСоставыРейсов.Маршрут КАК Маршрут,
	|	ТранспортнаяЛогистикаМаршруты.Водитель КАК Водитель,
	|	ТранспортнаяЛогистикаМаршруты.Автомобиль КАК Автомобиль,
	|	ТранспортнаяЛогистикаСоставыРейсов.Рейс КАК Рейс,
	|	ТранспортнаяЛогистикаСоставыРейсов.НомерВРейсе КАК НомерВРейсе,
	|	ТранспортнаяЛогистикаМаршруты.Пробег КАК Пробег
	|ИЗ
	|	Документ.ТранспортнаяЛогистика.СоставыРейсов КАК ТранспортнаяЛогистикаСоставыРейсов
	|		ПОЛНОЕ СОЕДИНЕНИЕ Документ.ТранспортнаяЛогистика.Маршруты КАК ТранспортнаяЛогистикаМаршруты
	|		ПО ТранспортнаяЛогистикаСоставыРейсов.Маршрут = ТранспортнаяЛогистикаМаршруты.Маршрут
	|		ПОЛНОЕ СОЕДИНЕНИЕ Документ.ТранспортнаяЛогистика.ДокументыМаршрута КАК ТранспортнаяЛогистикаДокументыМаршрута
	|		ПО ТранспортнаяЛогистикаСоставыРейсов.GUID = ТранспортнаяЛогистикаДокументыМаршрута.GUID
	|ГДЕ
	|	ТранспортнаяЛогистикаДокументыМаршрута.Ссылка = &Ссылка
	|	И ТранспортнаяЛогистикаМаршруты.Ссылка = &Ссылка
	|	И ТранспортнаяЛогистикаСоставыРейсов.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТранспортнаяЛогистикаСоставыРейсов.Маршрут,
	|	ТранспортнаяЛогистикаСоставыРейсов.Рейс,
	|	ТранспортнаяЛогистикаСоставыРейсов.НомерВРейсе,
	|	ТранспортнаяЛогистикаСоставыРейсов.АдресПредставление,
	|	ТранспортнаяЛогистикаСоставыРейсов.ВремяНачала,
	|	ТранспортнаяЛогистикаСоставыРейсов.ВремяОкончания,
	|	ТранспортнаяЛогистикаСоставыРейсов.Ссылка.Организация,
	|	ТранспортнаяЛогистикаДокументыМаршрута.Документ,
	|	ВЫБОР
	|		КОГДА ТранспортнаяЛогистикаДокументыМаршрута.Документ.Грузоотправитель <> ТранспортнаяЛогистикаДокументыМаршрута.Документ.Организация
	|				И ТранспортнаяЛогистикаДокументыМаршрута.Документ.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ТранспортнаяЛогистикаДокументыМаршрута.Документ.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ТранспортнаяЛогистикаДокументыМаршрута.Документ.Грузоотправитель
	|		ИНАЧЕ ТранспортнаяЛогистикаДокументыМаршрута.Документ.Организация
	|	КОНЕЦ,
	|	ТранспортнаяЛогистикаСоставыРейсов.ТочкаМаршрута,
	|	ТранспортнаяЛогистикаМаршруты.Водитель,
	|	ТранспортнаяЛогистикаМаршруты.Автомобиль,
	|	ТранспортнаяЛогистикаМаршруты.Пробег";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() тогда
		
		ДвиженияТранспортнаяЛогистикаЗаказы = Движения.ТранспортнаяЛогистикаЗаказы;
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Для Каждого СтрокаТовары из Выборка.ДокументДвижения.Товары цикл
			
				НовоеДвижение = ДвиженияТранспортнаяЛогистикаЗаказы.Добавить();
				
				ЗаполнитьЗначенияСвойств(НовоеДвижение, Выборка);
				
				НовоеДвижение.Период = Дата;
				НовоеДвижение.Регистратор = Ссылка;
				НовоеДвижение.ВидДвижения = ВидДвиженияНакопления.Расход;
				НовоеДвижение.Номенклатура = СтрокаТовары.Номенклатура;
				НовоеДвижение.Количество = СтрокаТовары.Количество;
				НовоеДвижение.Тоннаж = СтрокаТовары.Номенклатура.ВесЕдиницы * СтрокаТовары.Количество;
				НовоеДвижение.Наценка = СтрокаТовары.Наценка;
				НовоеДвижение.НомерВРейсе = Выборка.НомерВРейсе;
				
			КонецЦикла;
			
		КонецЦикла;
		
		ДвиженияТранспортнаяЛогистикаЗаказы.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// Заполнение даты документа
	ФиксированноеВремяДокументов.УстановитьВремяНовогоДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

