
&НаКлиенте
Процедура ТЛ_ПриОткрытииПосле(Отказ)
	Если Параметры.Ключ.Пустая() 
		и не Объект.ТЛ_Грузополучатель = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка") Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ОтветНаВопросОчиститьГрузополучателя", ЭтотОбъект),
                       "Поле ""Грузополучатель"" заполнено значением - "+Строка(Объект.ТЛ_Грузополучатель)+
		" на основании скопированного документа! Очистить поле ""Грузополучатель""?", РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросОчиститьГрузополучателя(РезультатВопроса, ПараметрыЗаписи) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ТЛ_Грузополучатель = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"); 
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТЛ_КонтрагентПриИзмененииПосле(Элемент)
	ПроставитьАдресДоставкиЛогистикаНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ТЛ_ТЛ_ГрузоотправительПриИзмененииПосле(Элемент)
	ПроставитьАдресДоставкиЛогистикаНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПроставитьАдресДоставкиЛогистикаНаСервере()
	Если ЗначениеЗаполнено(Объект.ТЛ_Грузополучатель) тогда
		Объект.ТЛ_АдресЗакупки = ТранспортнаяЛогистикаСервер.ПолучитьАдресКонтрагентаДляЛогистики(Объект.ТЛ_Грузополучатель);
	ИначеЕсли ЗначениеЗаполнено(Объект.Контрагент) тогда
		Объект.ТЛ_АдресЗакупки = ТранспортнаяЛогистикаСервер.ПолучитьАдресКонтрагентаДляЛогистики(Объект.Контрагент);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ТЛ_ДоставкаНажатиеПосле(Элемент)
	Форма = ЭтаФорма;
	
	Если НЕ Форма.ТолькоПросмотр Тогда
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",                 Форма.ТолькоПросмотр);
	ПараметрыФормы.Вставить("Организация",                    Объект.Организация);
	ПараметрыФормы.Вставить("Контрагент",                     Объект.Контрагент);
	ПараметрыФормы.Вставить("Грузоотправитель",               Объект.ТЛ_Грузоотправитель);
	ПараметрыФормы.Вставить("Грузополучатель",                Объект.ТЛ_Грузополучатель);
	ПараметрыФормы.Вставить("ДатаДокумента",                  Объект.Дата);
	ПараметрыФормы.Вставить("АдресДоставки",                  Объект.ТЛ_АдресЗакупки);
	ПараметрыФормы.Вставить("ПеревозкаАвтотранспортом",       Объект.ТЛ_ПеревозкаАвтотранспортом);
	
	
	
	ОткрытьФорму("Документ.Доверенность.Форма.ФормаДоставка", ПараметрыФормы, Форма);

КонецПроцедуры


&НаКлиенте
Процедура ТЛ_ОбработкаВыбораПосле(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Документ.Доверенность.Форма.ФормаДоставка" Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма.Объект, ВыбранноеЗначение);
		ЭтаФорма.Объект.ТЛ_Грузоотправитель = ВыбранноеЗначение.Грузоотправитель;
		ЭтаФорма.Объект.ТЛ_Грузополучатель = ВыбранноеЗначение.Грузополучатель;
		ЭтаФорма.Объект.ТЛ_АдресЗакупки = ВыбранноеЗначение.АдресДоставки;
		ЭтаФорма.Объект.ТЛ_ПеревозкаАвтотранспортом = ВыбранноеЗначение.ПеревозкаАвтотранспортом;
		
		ЭтаФорма.Модифицированность = Истина;

	КонецЕсли;	

КонецПроцедуры


&НаКлиенте
Процедура ТЛ_ПередЗаписьюПеред(Отказ, ПараметрыЗаписи)
	Если Объект.ТЛ_ПеревозкаАвтотранспортом тогда
		ЕстьВес = истина;
		Для Каждого Стр из Объект.Товары Цикл
			Если Стр.Вес = 0 тогда
				ЕстьВес = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЕстьВес тогда
			ПоказатьПредупреждение(,"При вывозе автотранспортом необходимо заполнить вес в каждой строке!");
			Отказ = истина;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

